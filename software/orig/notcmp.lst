
                              .PAGE  'DOCUMENTATION'
    3                ;        COPYRIGHT 1977 BY MICRO TECHNOLOGY UNLIMITED, BOX 4596,
    4                ;        MANCHESTER, NEW HAMPSHIRE 03108
    5                ;        PROGRAM WRITTEN BY HAL CHAMBERLIN
    6
    7                ;        THIS PROGRAM COMPILES A SIMPLIFIED SUBSET OF THE NOTRAN MUSIC
    8                ;        LANGUAGE INTO AN OBJECT CODE STRING FOR PLAYING BY THE KIM
    9                ;        NOTRAN INTERPRETER.
   10
   11                ;        LINES OF NOTRAN SOURCE CODE ARE PROCESSED BY THE COMPILER AND
   12                ;        CONVERTED INTO A SERIES OF OBJECT CODE BYTES WHICH ARE STORED
   13                ;        IN MEMORY AND LINES OF OBJECT CODE LISTING WHICH ARE PRINTED BY
   14                ;        AN OUTPUT DEVICE.  IF A CONTROLLABLE INPUT DEVICE IS AVAILABLE
   15                ;        (ONE THAT CAN BE STARTED AND STOPPED UNDER PROGRAM CONTROL)
   16                ;        THEN ONLY ONE LINE OF SOURCE CODE NEEDS TO BE IN MEMORY AT
   17                ;        ONCE.  STORAGE SPACE IS ALSO REQUIRED FOR A SMALL SYMBOL TABLE
   18                ;        WHICH NEEDS 3 BYTES PER SYMBOL.  SEE THE PROGRAM LISTING FOR
   19                ;        ADDRESS VECTORS AND SIZES OF THE SYMBOL TABLE, OBJECT CODE
   20                ;        AREA, LINE INPUT BUFFER, AND LINE OUTPUT BUFFER.
   21
   22                ;        NOTRAN SOURCE IS MADE UP OF LINES.  A LINE MAY HAVE AN OPTIONAL
   23                ;        IDENTIFIER AND MAY CONSIST OF ONE OR MORE SPECIFICATIONS.  THE
   24                ;        IDENTIFIER, IF PRESENT, MUST BE IN THE FIRST CHARACTER POSITION
   25                ;        OF THE LINE.  AN IDENTIFIER IS A UNIQUE NUMBER BETWEEN 1 AND
   26                ;        255.  IF THERE IS NO IDENTIFIER, THE FIRST CHARACTER POSITION
   27                ;        MUST BE BLANK.  IF THE FIRST CHARACTER OF A LINE IS AN *, THEN
   28                ;        THE ENTIRE LINE IS TREATED AS A COMMENT.
   29
   30                ;        A SPECIFICATION MAY EITHER BE A KEYWORD SPECIFICATION OR A NOTE
   31                ;        SPECIFICATION.  A KEYWORD SPECIFICATION MAY BE EITHER
   32                ;        EXECUTABLE IF OBJECT CODE BYTES ARE GENERATED FOR IT OR
   33                ;        NON-EXECUTABLE IF IT ONLY INFLUENCES FUTURE COMPILER ACTIONS.  A
   34                ;        KEYWORD SPECIFICATION CONSISTS OF A THREE LETTER KEYWORD
   35                ;        FOLLOWED BY OPTIONAL PARAMETERS SEPARATED BY COMMAS.  A NOTE
   36                ;        SPECIFICATION CONSISTS OF A STRING OF CHARACTERS TO BE DEFINED
   37                ;        LATER.  MULTIPLE SPECIFICATIONS ON ONE LINE MUST BE SEPARATED
   38                ;        BY SEMICOLONS.  ANY NUMBER OF BLANKS (INCLUDING ZERO) MAY BE
   39                ;        INSERTED BEFORE AND AFTER SEMICOLONS, BETWEEN A KEYWORD AND ITS
   40                ;        PARAMETERS, AND BEFORE THE FIRST SPECIFICATION ON A LINE,
   41                ;        BLANKS MAY NOT APPEAR IN THE MIDDLE OF A NOTE SPECIFICATION.
   42                ;        MAXIMUM INPUT LINE LENGTH IS 72 CHARACTERS.
   43
   44                ;        DUE TO THE ONE-PASS SIMPLIFIED NATURE OF THE COMPILER, FORWARD
   45                ;        REFERENCING IN JUMP AND JUMP TO SUBROUTINE STATEMENTS IS NOT
   46                ;        SUPPORTED.  A BLOCK OF STATEMENTS MAY HOWEVER BE DEFINED AND A
   47                ;        JUMP CREATED TO SKIP FORWARD OVER IT.  THE BEGINNING OF THE
   48                ;        BLOCK IS DEFINED BY A "SUB" STATEMENT AND THE END IS DEFINED BY
   49                ;        AN "ESB" STATEMENT.  A JUMP IS CREATED AT THE "SUB" STATEMENT
   50                ;        WHICH IS DIRECTED TO THE STATEMENT FOLLOWING THE "ESB".  THIS
   51                ;        IN EFFECT DEFINES A FORWARD REFERENCE JUMP THAT MAY BE USED TO
   52                ;        JUMP OVER SUBROUTINE DEFINITIONS.  ONLY ONE "SUB-ESB" PAIR MAY
   53                ;        BE ACTIVE AT A TIME.
   54
   55                ;        THE KEYWORD SPECIFICATIONS RECOGNIZED ARE AS FOLLOWS (X IS A
   56                ;        NUMERICAL PARAMETER):
   57                ;               NVC X        DEFINE NUMBER OF VOICES TO MIX, X IS IN
   58                ;                            RANGE IF 1-4 (SEE INTERPRETER LISTING)
   59                ;                            (EXECUTABLE)
   60                ;               ACT X,X,..,X ACTIVATE VOICE(S) X, X IN RANGE OF 1-4,
   61                ;                            (EXECUTABLE)
   62                ;               DCT X,X,..,X DEACTIVATE VOICE(S) X (EXECUTABLE)
   63                ;               WAV X,Y      ASSIGN WAVEFORM X TO VOICE Y. X IN RANGE OF
   64                ;                            1-16, Y IN RANGE OF 1-4
   65                ;                            (NOTE: CORRESPONDANCE BETWEEN WAVEFORM
   66                ;                            NUMBERS AND ACTUAL WAVEFORMS DEFINED WHEN
   67                ;                            THE INTERPRETER IS RUN)
   68                ;                            (NON-EXECUTABLE) .
   69                ;               TPO X        SET TEMPO TO X, X IN RANGE OF 1-255
   70                ;                            QUARTER NOTE DURATION (MILLISECONDS) =
   71                ;                            X*5.472 (EXECUTABLE)
   72                ;               ABS          FORCES ABSOLUTE PITCH IN OBJECT CODE OF THE
   73                ;                            NEXT NOTE SOUNDED BY EACH VOICE
   74                ;                            (NON-EXECUTABLE)
   75                ;               JMP X        UNCONDITIONAL JUMP TO THE LINE WHOSE
   76                ;                            IDENTIFIER MATCHES X WHILE PLAYING.
   77                ;                            (EXECUTABLE)
   78                ;               JSR X        JUMP TO MUSICAL SUBROUTINE TO THE LINE
   79                ;                            WHOSE IDENTIFIER MATCHES X WHILE PLAYING.
   80                ;                            (EXECUTABLE)
   81                ;               RTS          RETURN FROM MUSICAL SUBROUTINE (EXECUTABLE)
   82                ;               SUB          DEFINE BEGINNING OF SKIPPED STATEMENT BLOCK
   83                ;                            (EXECUTABLE)
   84                ;               ESB          DEFINE END OF SKIPPED STATEMENT BLOCK
   85                ;                            (EXECUTABLE)
   86                ;               END          END OF NOTRAN SOURCE (EXECUTABLE)
   87
   88
   89                ;        NOTE SPECIFICATIONS HAVE THE FOLLOWING FORMAT:
   90
   91                ;        (OPTIONAL VOICE DIGIT)(NOTE LETTER) (OPTIONAL # OR @) (OPTIONAL
   92                ;        OCTAVE NUMBER) (DURATION LETTER) (OPTIONAL DURATION MODIFIER)
   93
   94                ;        THE INDIVIDUAL FIELDS ARE AS FOLLOWS:
   95
   96                ;        OPTIONAL VOICE DIGIT A DIGIT IN THE RANGE OF 1 TO 4. DOES NOT
   97                ;                              REALLY DO ANYTHING BUT WHEN PRESENT IS
   98                ;                              COMPARED WITH THE VOICE NUMBER THAT THE
   99                ;                              INTERPRETER WOULD ACTUALLY ASSIGN TO THIS
  100                ;                              NOTE AND AN ERROR IS SIGNALLED IF THERE
  101                ;                              IS A MISMATCH.
  102                ;        NOTE LETTER           A, B, C, D, E, F, G ARE LEGAL
  103                ;        OPTIONAL # OR @       # FOR A HALF-STEP INCREMENT IN PITCH
  104                ;                              @ FOR A HALF-STEP DECREMENT IN PITCH
  105                ;        OPTIONAL OCTAVE NUMBER IF PRESENT SPECIFIES THE OCTAVE, RANGE
  106                ;                              IS 1-6, C4 IS MIDDLE C, C6 IS HIGHEST
  107                ;                              ALLOWABLE NOTE, AN OCTAVE SPANS FROM C TO
  108                ;                              B. IF NOT PRESENT, THE OCTAVE LAST
  109                ;                              PLAYED BY THE VOICE IS USED.
  110                ;        DURATION LETTER       W = WHOLE, H = HALF, Q = QUARTER,
  111                ;                              E = EIGHTH, S = SIXTEENTH, T = THIRTY-
  112                ;                              SECOND
  113                ;        OPTIONAL DURATION MODIFIER  . SPECIFIES 3/2 OF THE SPECIFIED
  114                ;                              DURATION, 3 SPECIFIES 2/3 OF THE
  115                ;                              SPECIFIED DURATION.  W.  W3  T3 NOT
  116                ;                              AVAILABLE.
  117
  118                ;                      RULES FOR ASSIGNING NOTES TO VOICES
  119
  120                ;        FOR PURPOSES OF THE KIM MUSIC SYSTEM, MUSIC IS BROKEN UP INTO A
  121                ;        SERIES OF EVENTS.  AN EVENT IS DEFINED AS THE INTERVAL BETWEEN
  122                ;        *ANY* CHANGE IN THE NOTES OF A CHORD BEING PLAYED.  THE
  123                ;        ORIGINAL FORM OF THE MUSIC SYSTEM REQUIRED 5 BYTES FOR EVERY
  124                ;        EVENT EVEN IF ONLY ONE NOTE OF A 4 NOTE CHORD CHANGED BETWEEN
  125                ;        TWO SUCCESSIVE EVENTS.  LETS ASSUME FOR THE MONENT THE
  126                ;        EXISTENCE OF A LONG STRING OF EVENTS WITH NO CONTROL
  127                ;        SPECIFICATIONS (TEMPO CHANGE, ETC.) INTERSPERSED.  AT THE
  128                ;        CONCLUSION OF THE PREVIOUS EVENT THE INTERPRETER WILL FETCH
  129                ;        NOTE CODE BYTES FROM THE CODE STRING ONE-AT-A-TIME TO BUILD UP
  130                ;        4 NOTES FOR THE NEXT EVENT.  HOWEVER IF, AS IS OFTEN THE CASE
  131                ;        IN MUSIC, THE NOTES OF THE PREVIOUS EVENT WERE OF DIFFERENT
  132                ;        DURATIONS, ONLY THE CHANGED NOTES NEED TO BE FETCHED FROM THE
  133                ;        CODE STRING.  THE "UNEXPIRED" NOTES FROM THE PREVIOUS EVENT CAN
  134                ;        BE CARRIED OVER TO THE CURRENT EVENT.  THE RULE FOR ASSIGNING
  135                ;        VOICE NUMBERS (1, 2, 3, OR 4 WHICH HAVE BEEN EQUATED WITH
  136                ;        DIFFERENT WAVEFORMS) IS THAT THEY ARE ASSIGNED TO CODE BYTES
  137                ;        SEQUENTIALLY STARTING WITH VOICE 1.  HOWEVER, IF VOICE 1 IS
  138                ;        BEING CARRIED OVER FROM THE PREVIOUS EVENT THEN IT IS SKIPPED
  139                ;        AND VOICE 2 IS TRIED.  THIS PROCESS, WHICH IS CALLED "EVENT
  140                ;        BUILDING" CONTINUES UNTIL ALL "EXPIRED" VOICES ARE ASSIGNED NEW
  141                ;        NOTES FROM THE CODE STRING.  IF A VOICE IS TO BE SILENT FOR THE
  142                ;        EVENT, A REST SPECIFICATION IS PLACED IN THE CODE STRING.  IF A
  143                ;        VOICE IS TO BE SILENT FOR AN EXTENDED PERIOD OF TIME, IT MAY BE
  144                ;        "DEACTIVATED".  INACTIVE VOICES ARE SKIPPED WHEN NOTES ARE
  145                ;        ASSIGNED TO VOICES.
  146
  147                ;        AFTER THE NOTES ARE ASSIGNED, THE EVENT LENGTH IS COMPUTED.
  148                ;        THE EVENT LENGTH IS SIMPLY THE DURATION OF THE VOICE WITH THE
  149                ;        SHORTEST REMAINING DURATION.  THE INTERPRETER THEN PLAYS THE
  150                ;        CHORD THAT WAS SET UP FOR THIS SHORTEST DURATION AND THEN LOOPS
  151                ;        BACK TO SET UP FOR THE NEXT EVENT.
  152
  153                ;        CONTROL SPECIFICATIONS SUCH AS A TEMPO CHANGE SHOULD ONLY OCCUR
  154                ;        BETWEEN THE GROUPS OF NOTE CODES THAT COMPRISE AN EVENT.  IF
  155                ;        THE INTERPRETER SEES SUCH A CONTROL COMMAND WHILE BUILDING AN
  156                ;        EVENT, IT ABORTS THE PARTIALLY BUILT EVENT, PROCESSES THE
  157                ;        CONTROL COMMAND, AND THEN STARTS ANEW BUILDING AN EVENT.  THE
  158                ;        COMPILER ALERTS THE USER THROUGH AN ERROR MESSAGE IF AN ATTEMPT
  159                ;        HAS BEEN MADE TO INSERT A CONTROL COMMAND IN THE MIDDLE OF AN
  160                ;        EVENT GROUP.  THE SAME IS TRUE IF AN IDENTIFIER IS PLACED ON A
  161                ;        NOTE SPECIFICATION THAT IS IN THE MIDDLE OF AN EVENT GROUP.
  162
  163                ;        IN ORDER TO CONSERVE SPACE IN THE OBJECT CODE FORMAT AN
  164                ;        EXPLICIT VOICE NUMBER SPECIFICATION IS NOT INCLUDED IN THE NOTE
  165                ;        CODES.  THE ASSIGNMENT OF VOICES TO NOTE CODES IS PERFORMED BY
  166                ;        THE NOTRAN OBJECT CODE INTERPRETER.  IN ORDER TO MINIMIZE
  167                ;        CODING ERRORS BY THE NOTRAN USER, THE COMPILER DUPLICATES THE
  168                ;        ASSIGNMENT ACTIONS OF THE INTERPRETER AND SIGNALS ANY MISMATCH
  169                ;        BETWEEN THE VOICE THAT WOULD BE ASSIGNED BY THE INTERPRETER AND
  170                ;        THE VOICE INTENDED BY THE USER.
  171
  172                ;        ACTUALLY THIS COMPILER IS MORE LIKE AN ASSEMBLER IN THAT NOTE
  173                ;        AND CONTROL SPECIFICATIONS ARE TRANSLATED AND PASSED
  174                ;        ONE-FOR-ONE INTO OBJECT CODE FOR THE NOTRAN INTERPRETER.
  175                ;        CERTAINLY A MORE SOPHISTICATED COMPILER IS POSSIBLE WHICH, FOR
  176                ;        EXAMPLE, MIGHT ALLOW THE USER TO SPECIFY THE MUSIC
  177                ;        "HORIZONTALLY" ONE VOICE LINE AT A TIME RATHER THAN
  178                ;        "VERTICALLY" ONE CHORD AT A TIME.
  179
  180                ;        ERROR MESSAGES ARE AS FOLLOWS:
  181
  182                ;        ER-01 ARGUMENT OUT OF RANGE, EITHER TOO BIG OR TOO SMALL
  183                ;        ER-02 UNDEFINED IDENTIFIER IN A JMP OR JSR SPECIFICATION
  184                ;        ER-03 IDENTIFIER HAS ALREADY BEEN USED, IT IS IGNORED
  185                ;        ER-04 SYMBOL TABLE OVERFLOW, THE IDENTIFIER IS IGNORED
  186                ;        ER-05 OBJECT CODE OVERFLOW, THE OBJECT CODE BYTE IS NOT STORED
  187                ;        ER-06 INCOMPREHENSIBLE SPECIFICATION, SKIP TO NEXT
  188                ;              SPECIFICATION OR STATEMENT
  189                ;        ER-07 SPECIFIED VOICE NUMBER AND ASSUMED VOICE NUMBER DO NOT
  190                ;              AGREE, ASSUMED VOICE NUMBER IS USED
  191                ;        ER-08 NOTE PITCH OUT OF RANGE, EITHER TOO HIGH OR TOO LOW OR
  192                ;              OCTAVE NUMBER OMITTED AND VOICE HAS NOT BEEN PREVIOUSLY
  193                ;              SOUNDED, USE HIGHEST AVAILABLE PITCH
  194                ;        ER-09 ILLEGAL DURATION, NOTE IS SKIPPED
  195                ;        ER-10 EXECUTABLE CONTROL SPECIFICATION IN MIDDLE OF EVENT
  196                ;              BUILD, PARTIAL EVENT IS SKIPPED AND CONTROL SPEC IS
  197                ;              EXECUTED.
  198                ;        ER-11 IDENTIFIER OCCURS IN THE MIDDLE OF AN EVENT, THE
  199                ;              IDENTIFIER IS PROCESSED.
  200                ;        ER-12 ATTEMPT MADE TO NEST SUB-ESB PAIR, SPECIFICATION IGNORED
  201                ;        ER-13 ESB WITHOUT A MATCHING SUB SPECIFICATION, THE ESB IS
  202                ;              IGNORED
  203                ;        ER-14 HANGING SUB AT END OF SONG
  204                ;        ER-15 NOTES ENCOUNTERED BUT NO VOICES ARE ACTIVE, COMPILATION
  205                ;              ABORTED
  206
  207
  208                ;        SINCE THE COMPILER IS NOT VERY SOPHISTICATED, OCCASIONALLY AN
  209                ;        ERROR MESSAGE MAY BE INAPPROPRIATE OR SEPARATED FROM THE ACTUAL
  210                ;        ERROR LOCATION.  BE ON THE LOOKOUT FOR ANYTHING WHEN THE ERROR
  211                ;        CODE IS 1, 6, 8, OR 9.
  212
                              .PAGE  'CONSTANTS AND DATA'
  213 0000                    .=     0            ; KEEP ALL CONSTANTS AND DATA IN PAGE 0
  214
  215 1C22           KIMMON   =      X'1C22       ; ENTRY POINT TO KIM MONITOR
  216 1C00           KIMSAV   =      X'1C00       ; SAVE STATUS ENTRY POINT TO KIM MONITOR
  217 1E5A           KIMICH   =      X'1E5A       ; KIM TTY INPUT A CHARACTER INTO A, KEEPS X
  218 1EA0           KIMOCH   =      X'1EA0       ; KIM TTY OUTPUT A CHAR FROM A, KEEPS X
  219 1ED4           KIMDLY   =      X'1ED4       ; KIM TTY DELAY ONE BIT TIME, KEEPS X
  220
  221                ;        NOTE: THESE ARE PARAMETERS THAT MUST BE SET UP BY THE USER
  222                ;        ACCORDING TO THE SYSTEM MEMORY CONFIGURATION
  223                ;        DEFAULT SETTINGS SHOWN FOR 4K EXPANSION STARTING AT 2000 (HEX)
  224                ;        AND USE OF DEFAULT 1/0 ROUTINES
  225
  226 0000 0003      INBFA:   .WORD  X'300        ; INPUT BUFFER ADDRESS, 73 BYTES USED
  227 0002 4903      OUBFA:   .WORD  X'300+73     ; OUTPUT BUFFER ADDRESS, 74 BYTES USED
  228 0004 9303      SYMTA:   .WORD  X'300+73+74  ; SYMBOL TABLE ADDRESS
  229 0006 2F26      CODEA:   .WORD  CMPEND       ; OBJECT CODE AREA ADDRESS
  230 0008 D009      CODELN:  .WORD  X'2FFF-CMPEND; MAXIMUM NUMBER OF MUSIC OBJECT CODE BYTES
  231                                             ; ALLOWED
  232 000A 21        SYMTLN:  .BYTE  100-1/3      ; MAXIMUM NUMBER OF SYMBOLS ALLOWED, 3
  233                                             ; BYTES OF MEMORY USED FOR EACH SYMBOL
  234                                             ; PLUS 1 FOR AN END MARK
  235
  236                ;        THESE ADDRESSES MAY BE ALTERED BY THE USER TO USE ALTERNATE I/O
  237                ;        ROUTINES IF A TELETYPE IS NOT AVAILABLE
  238
  239 000B 0102      INP:     .WORD  DFIN         ; INPUT ROUTINE ADDRESS
  240 000D 0002      INPINT:  .WORD  DFINIT       ; INPUT ROUTINE INITIALIZE ADDRESS
  241 000F 5402      OUT:     .WORD  DFOU         ; OUTPUT ROUTINE ADDRESS
  242 0011 5302      OUTINT:  .WORD  DFOUIT       ; OUTPUT ROUTINE INITIALIZE ADDRESS
  243 0013 6802      ERR:     .WORD  DFERR        ; ERROR NOTIFICATION ROUTINE, CALLED WHEN
  244                                             ; AN ERROR IS ENCOUNTERED AND LISTING IS
  245                                             ; SUPPRESSED
  246
  247                ;        LISTING CONTROL PARAMETERS
  248
  249 0015 01        LIST:    .BYTE  1            ; MUSIC OBJECT CODE LISTING IN HEX IF NOT 0
  250 0016 01        ECHO:    .BYTE  1            ; ECHO INPUT STATEMENT IN LISTING IF NOT 0
  251
  252                ;         ADDITIONAL CONSTANTS AND VARIABLES USED BY THE COMPILER
  253
  254 0017 00        INBFPT:  .BYTE  0            ; INPUT BUFFER POINTER, OFFSET FROM (INBFA}
  255 0018 00        OUBFPT:  .BYTE  0            ; OUTPUT BUFFER POINTER,OFFSET FROM (OUBFA)
  256 0019 0000      CODEPT:  .WORD  0            ; OBJECT CODE POINTER, ABS ADDRESS
  257 001B 0000      CODECT:  .WORD  0            ; COUNT OF OBJECT CODE BYTES GENERATED
  258 001D 0000      SYMTPT:  .WORD  0            ; SYMBOL TABLE POINTER, ABS ADDRESS
  259 001F 00        SYMTCT:  .BYTE  0            ; COUNT OF SYMBOLS ENTERED INTO THE TABLE
  260 0020 0000      INDJMP:  .WORD  0            ; TEMPORARY STORAGE FOR INDIRECT JUMP
  261 0022 00        NBF:     .BYTE  0            ; RESULT OF NUMBER COLLECT ROUTINE
  262 0023 00        ENDFLG:  .BYTE  0            ; SET NON-ZERO WHEN END STATEMENT PROC.
  263 0024 0000      SUBSKP:  .WORD  0            ; ADDR OF "SUB" STATEMNENT IN OBJECT CODE
  264
  265                                             ; RETURN PARAMETERS FROM NOTE COLLECT
  266 0026 00        NTVOIC:  .BYTE  0            ; VOICE NUMBER, IF SPECIFIED, 0 OTHERWISE
  267 0027 00        NTDUR:   .BYTE  0            ; DURATION CODE FOR THE NOTE
  268 0028 00        NTDURX:  .BYTE  0            ; DURATION TIME FOR THE NOTE
  269 0029 00        NTPTC:   .BYTE  0            ; COMPOSITE PITCH FOR THE NOTE
  270 002A 00        NTOCT:   .BYTE  0            ; OCTAVE NUMBER FOR THE NOTE
  271
  272 002B 00        NTERR:   .BYTE  0            ; ERROR FLAG FOR NOTE COLLECT ROUTINE
  273 002C 00        NMERR:   .BYTE  0            ; ERROR FLAG FOR NUMBER COLLECT ROUTINE
  274
  275                                             ; DATA FOR PROCESSING MUSICAL EVENTS
  276 002D 00        EVTBLD:  .BYTE  0            ; EVENT BEING BUILT IF NOT ZERO
  277 002E 00        DUR:     .BYTE  0            ; DURATION OF SHORTEST NOTE
  278 002F 00        VPPNT:   .BYTE  0            ; VOICE NUMBER BEING PROCESSED
  279 0030           VXPTC:   .=.+   4            ; CURRENT VOICE COMPOSITE PITCHES
  280 0034           VXOCT:   .=.+   4            ; CURRENT VOICE PITCH OCTAVES
  281 0038           VXABS:   .=.+   4            ; FORCE ABSOLUTE PITCH FLAGS, NOT 0 = FORCE
  282 003C           VXWAV:   .=.+   4            ; CURRENT VOICE WAVEFORM NUMBERS
  283 0040           VXDUR:   .=.+   4            ; CURRENT VOICE DURATIONS
  284
  285 0044 00        KWTP:    .BYTE  0            ; MISCELLANEOUS TEMPORARY STORAGE FOR LOW
  286 0045 00        KWMC:    .BYTE  0            ; LEVEL ROUTINES
  287 0046 00        CDSAV:   .BYTE  0
  288
                              .PAGE  'DEFAULT INPUT, OUTPUT, AND ERROR ROUTINES'
  289                ;        DFIN DEFAULT INPUT ROUTINE
  290
  291                ;        THIS INPUT ROUTINE IS DESIGNED TO BE USED WITH A SERIAL DEVICE
  292                ;        SUCH AS A #33 TELETYPE WITH PAPER TAPE.  THE PAPER TAPE SHOULD
  293                ;        BE FORMATTED INTO LINES WITH THE CONTROL CHARACTER SEQUENCE [CR
  294                ;        LF DC1 DEL] BETWEEN THE TEXT OF EACH LINE.  THE [DC1 DEL]
  295                ;        SEQUENCE IS USED TO STOP THE TTY READER AFTER AN INPUT LINE HAS
  296                ;        BEEN READ.  TO INITIATE READING OF A LINE THIS ROUTINE FIRST
  297                ;        SENDS A DC2 CONTROL CHARACTER WHICH STARTS THE TTY READER.
  298                ;        TEXT READ IN IS STORED IN THE INPUT BUFFER.  ANY CONTROL
  299                ;        CHARACTERS PRECEEDING THE TEXT ARE THROWN AWAY SO THAT THE
  300                ;        FIRST CHARACTER IN THE BUFFER IS THE FIRST PRINTABLE CHARACTER
  301                ;        IN THE TEXT.  EXCESS CHARACTERS IN THE INPUT LINE BEYOND 72 ARE
  302                ;        THROWN AWAY UNTIL A "CR" IS ENCOUNTERED.  BS (X'08) OR < (X'5F)
  303                ;        APPEARING IN THE STRING CAUSES THE PREVIOUS CHARACTER IN THE
  304                ;        BUFFER TO BE DELETED THUS EFFECTING A BACKSPACE.  LOWER CASE
  305                ;        ALPHABETIC CHARACTERS ARE TRANSLATED TO THEIR UPPER CASE
  306                ;        EQUIVALENT.  WHEN THE DC1 CHARACTER IS READ AND ECHOED BACK
  307                ;        THROUGH THE KIM TTY PORT, THE TTY READER STOPS THUS ALLOWING
  308                ;        TIME TO COMPILE THE LINE AND PRINT THE LISTING IF ENABLED.  THE
  309                ;        TEXT STORED IN THE INPUT BUFFER IS TERMINATED WITH A "CR"
  310                ;        CHARACTER.  THE KIM TTY INPUT AND OUTPUT ROUTINES ARE UTILIZED
  311                ;        FOR INDIVIDUAL CHARACTER INPUT AND OUTPUT.  NOTE THAT TTY MODE
  312                ;        OF OPERATION (JUMPER FROM A-XX TO A-XX ON THE KIM BOARD) MUST
  313                ;        BE SELECTED FOR THE TTY INPUT ROUTINE TO FUNCTION PROPERLY.
  314
  315                ;        THE USER MAY SUBSTITUTE HIS OWN INPUT ROUTINE.  THE COMPILER
  316                ;        ONLY REQUIRES THAT THE TEXT IN THE INPUT BUFFER START WITH THE
  317                ;        FIRST CHARACTER TO BE PROCESSED AND BE TERMINATED WITH A "CR".
  318                ;        THE USER ROUTINE MAY FREELY UTILIZE ALL OF THE REGISTERS, AND
  319                ;        ANY BASE PAGE LOCATIONS NOT APPEARING IN THE COMPILER LISTING.
  320
  321 0047                    .=     X'200        ; PUT DEFAULT I/O ROUTINES IN PAGE 2
  322
  323 0200 60        DFINIT:  RTS                 ; DUMMY INPUT INITIALIZE ROUTINE
  324
  325 0201 A200      DFIN:    LDX    #0           ; INITIALIZE INPUT BUFFER POINTER
  326 0203 A912               LDA    #X'12        ; TRANSMIT AN XON (DC2) TO START TTY READER
  327 0205 20A01E             JSR    KIMOCH
  328 0208 205A1E    DFIN1:   JSR    KIMICH       ; WAIT FOR AN INPUT CHARACTER
  329 020B C920               CMP    #X'20        ; TEST IF IT IS ANY KIND OF CONTROL CHAR
  330 020D 90F9               BCC    DFIN1        ; IGNORE IT IF SO
  331 020F C97F               CMP    #X'7F        ; IGNORE DEL ALSO
  332 0211 F0F5               BEQ    DFIN1
  333 0213 D00F               BNE    DFIN3        ; GO STORE FIRST TEXT CHARACTER
  334 0215 205A1E    DFIN2:   JSR    KIMICH       ; GET A TEXT CHARACTER
  335 0218 C97F               CMP    #X'7F        ; TEST IF DEL
  336 021A F0F9               BEQ    DFIN2        ; IGNORE IF SO
  337 021C C90D               CMP    #X'0D        ; TEST IF CR
  338 021E F024               BEQ    DFIN4        ; JUMP IF SO
  339 0220 E048               CPX    #72          ; TEST IF 72 TEXT CHARS ALREADY BEEN STORED
  340 0222 F0F1               BEQ    DFIN2        ; IGNORE BEYOND 72 UNTIL CR
  341 0224 C908      DFIN3:   CMP    #X'08        ; TEST IF IT IS A BACKSPACE CONTROL
  342 0226 F004               BEQ    DFIN32       ; JUMP IF IT IS
  343 0228 C93C               CMP    #'<'         ; TEST IF IT IS A BACKARROW
  344 022A B005               BNE    DFIN35       ; SKIP AHEAD IF NOT
  345 022C E000      DFIN32:  CPX    #0           ; EXECUTE BACKSPACE, TEST IF AT START OF
  346 022E F001               BEQ    DFIN35       ; BUFFER, NO BACKSPACE IF SO
  347 0230 CA                 DEX
  348 0231 C97B      DFIN35:  CMP    #X'7B        ; TEST IF LOWER CASE ALPHABETIC
  349 0233 B006               BCS    DFIN37       ; JUMP AHEAD IF NOT
  350 0235 C961               CMP    #X'61
  351 0237 9002               BCC    DFIN37       ; JUMP AHEAD IF NOT
  352 0239 E920               SBC    #X'20        ; TRANSLATE LOWER CASE TO UPPER CASE
  353 023B 48        DFIN37:  PHA                 ; STORE CHARACTER IN INPUT BUFFER
  354 023C 8A                 TXA
  355 023D A8                 TAY
  356 023E 68                 PLA
  357 023F 9100               STA    (INBFA),Y
  358 0241 E8                 INX                 ; INCREMENT BUFFER INDEX
  359 0242 D0D1               BNE    DFIN2        ; GO FOR NEXT CHARACTER
  360
  361 0244 48        DFIN4:   PHA                 ; STORE THE CR IN THE BUFFER
  362 0245 8A                 TXA
  363 0246 A8                 TAY
  364 0247 68                 PLA
  365 0248 9100               STA    (INBFA),Y
  366 024A A22C               LDX    #44          ; WAIT 4 CHARACTER TIMES AT CURRENT BAUD
  367 024C 20D41E    DFIN5:   JSR    KIMDLY       ; RATE TO AVOID POSSIBLE CONFLICT BETWEEN
  368 024F CA                 DEX                 ; INPUT TERMINATION SEQUENCE AND
  369 0250 D0FA               BNE    DFIN5        ; LISTING OUTPUT
  370 0252 60                 RTS                 ; RETURN WITH INPUT BUFFER LOADED
  371
  372
  373                ;        DFOU DEFAULT OUTPUT ROUTINE
  374
  375                ;        THIS OUTPUT ROUTINE IS DESIGNED TO BE USED WITH A SERIAL DEVICE
  376                ;        SUCH AS A #33 TELETYPE.  THE COMPILER SUPPLIES AN OUTPUT BUFFER
  377                ;        CONTAINING A FORMATTED CHARACTER STRING TERMINATED BY ASCII
  378                ;        "CR" AND "LF" CONTROL CHARACTERS.  THE LINE HAS A MAXIMUM OF 72
  379                ;        PRINTABLE CHARACTERS.  THE KIM TTY OUTPUT ROUTINE IS USED FOR
  380                ;        INDIVIDUAL CHARACTER OUTPUT.
  381
  382                ;        THE USER MAY SUBSTITUTE HIS OWN OUTPUT ROUTINE.  THE USER
  383                ;        ROUTINE MAY FREELY UTILIZE ALL OF THE REGISTERS, AND ANY BASE
  384                ;        PAGE LOCATIONS NOT APPEARING IN THE COMPILER LISTING.
  385
  386 0253 60        DFOUIT:  RTS                 ; DUMMY OUTPUT INITIALIZE ROUTINE
  387
  388 0254 A200      DFOU:    LDX    #0           ; INITIALIZE OUTPUT POINTER
  389 0256 8A        DFOUT1:  TXA                 ; GET A CHARACTER FROM THE BUFFER
  390 0257 A8                 TAY
  391 0258 B102               LDA    (OUBFA),Y
  392 025A C90A               CMP    #X'0A        ; TEST IF IT IS A LF
  393 025C F006               BEQ    DFOUT2       ; JUMP OUT IF SO
  394 025E 20A01E             JSR    KIMOCH       ; OUTPUT THE CHARACTER
  395 0261 E8                 INX                 ; INCREMENT OUTPUT POINTER
  396 0262 D0F2               BNE    DFOUT1       ; GO OUTPUT NEXT CHAR
  397 0264 20A01E    DFOUT2:  JSR    KIMOCH       ; OUTPUT THE LF
  398 0267 60                 RTS                 ; AND RETURN
  399
  400
  401                ;        DFERR DEFAULT ERROR ROUTINE TO BE USED WHEN LISTING IS
  402                ;        SUPPRESSED ERROR CODE IS IN A.
  403                ;        A "SAVE STATUS" CALL IS MADE TO THE KIM MONITOR WHERE THE USER
  404                ;        CAN EXAMINE THE ERROR CODE AND EITHER CONTINUE OR ABORT THE
  405                ;        COMPILATION.
  406
  407                ;        THE USER MAY SUPPLY HIS OWN ERROR ROUTINE.  THE INDEX REGISTERS
  408                ;        X AND Y, SHOULD BE SAVED WHILE THE ERROR REPORT IS BEING MADE
  409                ;        AND RESTORED BEFORE RETURNING TO THE COMPILER.
  410
  411 0268 4C001C    DFERR:   JMP    KIMSAV       ; GO TO KIM SAVE STATUS ENTRY POINT
  412 026B 60                 RTS                 ; RETURN TO NOTRAN COMPILER
  413
                              .PAGE  'INITIALIZATION ROUTINE'
  414 026C                    .=     X'2000       ; START IN EXTENDED MEMORY
  415
  416 2000 D8        NOTRAN:  CLD                 ; INSURE BINARY ARITHMETIC
  417 2001 A2F0               LDX    #X'F0        ; INITIALIZE STACK POINTER
  418 2003 9A                 TXS
  419 2004 A900               LDA    #0           ; ZERO SOME PARAMETERS
  420 2006 851F               STA    SYMTCT       ; NUMBER OF SYMBOLS IN TABLE
  421 2008 851B               STA    CODECT       ; NUMBER OF OBJECT CODE BYTES GENERATED
  422 200A 851C               STA    CODECT+1
  423 200C 8524               STA    SUBSKP       ; CLEAR SKIP ADDRESS IN SUB STATEMENT
  424 200E 8525               STA    SUBSKP+1
  425 2010 852D               STA    EVTBLD       ; TURN EVENT BEING BUILT FLAG OFF
  426 2012 8523               STA    ENDFLG       ; TURN END FLAG OFF
  427 2014 A8                 TAY                 ; CLEAR THE SYMBOL TABLE
  428 2015 9104               STA    (SYMTA),Y
  429 2017 852F               STA    VPPNT        ; INITIALIZE VOICE PROCESSING POINTER
  430 2019 AA                 TAX
  431 201A A900      NOTR1:   LDA    #0
  432 201C 953C               STA    VXWAV,X      ; DEFAULT TO WAVEFORM 1 ON ALL VOICES
  433 201E A9FF               LDA    #X'FF
  434 2020 9540               STA    VXDUR,X      ; DEACTIVATE ALL VOICES
  435 2022 9538               STA    VXABS,X      ; FORCE ABSOLUTE PITCH ON ALL VOICES
  436 2024 E8                 INX
  437 2025 E004               CPX    #4
  438 2027 D0F1               BNE    NOTR1
  439 2029 A506               LDA    CODEA        ; INITIALIZE OBJECT CODE POINTER
  440 202B 8519               STA    CODEPT
  441 202D A507               LDA    CODEA+1
  442 202F 851A               STA    CODEPT+1
  443 2031 20F325             JSR    INIT         ; INITIALIZE INPUT ROUTINE
  444 2034 20F625             JSR    OUIT         ; INITIALIZE OUTPUT ROUTINE
  445
                              .PAGE  'MAIN STATEMENT PROCESSOR'
  446
  447 2037 20ED25    STAT:    JSR    IN           ; READ AN INPUT LINE
  448 203A A000               LDY    #0
  449 203C A516               LDA    ECHO         ; TEST IF ECHO TO OUTPUT ENABLED
  450 203E D00C               BNE    STAT1        ; SKIP AHEAD IF SO
  451 2040 B100               LDA    (INBFA),Y    ; LOOK AT FIRST CHAR. OF INPUT LINE IF NOT
  452 2042 C92A               CMP    #'*'         ; TEST IF IT IS A COMMENT LINE
  453 2044 D018               BNE    STAT3        ; JUMP AHEAD IF NOT A COMMENT
  454 2046 A515               LDA    LIST         ; IF A COMMENT, SEE IF CODE LISTING ENABLED
  455 2048 F0ED               BEQ    STAT         ; IGNORE COMMENT IF LISTING DISABLED
  456 204A A000               LDY    #0           ; COPY INPUT BUFFER INTO OUTPUT BUFFER
  457 204C B100      STAT1:   LDA    (INBFA),Y    ; GET A CHARACTER FROM THE INPUT BUFFER
  458 204E 9102               STA    (OUBFA),Y    ; PUT IT INTO THE OUTPUT BUFFER
  459 2050 C8                 INY                 ; INCREMENT X TO NEXT CHARACTER
  460 2051 C90D               CMP    #X'0D        ; TEST IF A CARRIAGE RETURN WAS MOVED
  461 2053 F002               BEQ    STAT2        ; JUMP OUT IF SO
  462 2055 D0F5               BNE    STAT1        ; GO MOVE ANOTHER CHARACTER
  463 2057 A90A      STAT2:   LDA    #X'0A        ; PUT A LINE FEED AT END OF OUTPUT BUFFER
  464 2059 9102               STA    (OUBFA),Y
  465 205B 20F025             JSR    OU           ; PRINT AN OUTPUT LINE
  466
  467 205E A000      STAT3:   LDY    #0           ; INITIALIZE INPUT BUFFER POINTER
  468 2060 8417               STY    INBFPT
  469 2062 8418               STY    OUBFPT       ; INITIALIZE OUTPUT BUFFER POINTER
  470 2064 202A23             JSR    OULOC        ; GENERATE CURRENT LOCATION ON LISTING
  471 2067 B100               LDA    (INBFA),Y    ; GET FIRST CHARACTER OF INPUT LINE
  472 2069 C92A               CMP    #'*'         ; TEST IF COMMENT LINE
  473 206B F0CA               BEQ    STAT         ; GO FOR NEXT STATEMENT IF A COMMENT LINE
  474 206D C920               CMP    #' '         ; TEST IF A BLANK
  475 206F F03B               BEQ    STAT8        ; JUMP IF SO, NO IDENTIFIER FOR LINE
  476 2071 C930               CMP    #'0'         ; TEST IF A DIGIT
  477 2073 9004               BCC    STAT4        ; JUMP IF NOT
  478 2075 C93A               CMP    #'9'+1
  479 2077 9008               BCC    STAT6        ; JUMP IF IT IS A DIGIT
  480 2079 A906      STAT4:   LDA    #6           ; SIGNAL ER 6 IF NOT A *, DIGIT, OR BLANK
  481 207B 20F925    STAT5:   JSR    ER
  482 207E 4CC221             JMP    ESTAT        ; GO TO END OF STATEMENT PROCESSING
  483
  484 2081 A52D      STAT6:   LDA    EVTBLD       ; TEST IF IDENTIFIER IN MIDDLE OF EVENT
  485 2083 F005               BEQ    STAT65
  486 2085 A911               LDA    #X'11        ; GENERATE ER-11 IF SO
  487 2087 20F925             JSR    ER           ; AND PROCESS IDENTIFIER ANYWAY
  488 208A 20B724    STAT65:  JSR    NCOL         ; EVALUATE THE STATEMENT IDENTIFIER
  489 208D A52C               LDA    NMERR        ; TEST FOR OVERFLOW
  490 208F D0EA               BNE    STAT5        ; JUMP IF SO
  491 2091 A522               LDA    NBF          ; GET THE IDENTIFIER
  492 2093 F0E4               BEQ    STAT4        ; ZERO NOT ALLOWED FOR AN IDENTIFIER
  493 2095 206625             JSR    SYMSR        ; CHECK FOR DUPLICATE SYMBOL IN THE TABLE
  494 2098 9008               BCC    STAT7        ; JUMP IF NOT
  495 209A A903               LDA    #3           ; SIGNAL ER 3 IF DUPLICATE IDENTIFIER
  496 209C 20F925             JSR    ER
  497 209F 4CAC20             JMP    STAT8        ; CONTINUE PROCESSING THE STATEMENT
  498 20A2 209C25    STAT7:   JSR    SYMAD        ; ADD THE IDENTIFIER TO THE SYMBOL TABLE
  499 20A5 B005               BCS    STAT8        ; JUMP IF SUCCESSFUL ADD
  500 20A7 A904               LDA    #4           ; GENERATE ER-4 IF SYMBOL TABLE OVERFLOW
  501 20A9 20F925             JSR    ER
  502
  503                ;        PROCESS REMAINDER OF STATEMENT AFTER LEADING BLANK, IDENTIFIER,
  504                ;        OR SEMICOLON
  505
  506 20AC 20D225    STAT8:   JSR    NNB          ; MOVE TO NEXT NON-BLANK CHARACTER
  507 20AF 20F924             JSR    KWCOL        ; TRY TO RECOGNIZE A KEYWORD
  508 20B2 900D               BCC    STAT10       ; SKIP AHEAD IF NO KEYWORD
  509 20B4 BD2925             LDA    KWTAB,X      ; MOVE JUMP ADDRESS FROM KEYWORD TABLE TO
  510 20B7 8520               STA    INDJMP       ; INDIRECT JUMP POINTER IN PAGE 0
  511 20B9 BD2A25             LDA    KWTAB+1,X
  512 20BC 8521               STA    INDJMP+1
  513 20BE 6C2000             JMP    (INDJMP)     ; JUMP TO APPROPRIATE KEYWORD PROCESSING
  514                                             ; ROUTINE
  515
  516 20C1 20BF23    STAT10:  JSR    NTCOL        ; TRY TO RECOGNIZE A VALID NOTE OR REST
  517 20C4 A52B               LDA    NTERR        ; GET ERROR CODE
  518 20C6 F006               BEQ    STAT11       ; SKIP AHEAD IF NO ERROR
  519 20C8 20F925             JSR    ER           ; SIGNAL ERROR IF NTCOL DETECTED ONE
  520 20CB 4CAE21             JMP    ESPEC        ; GO TO END OF SPECIFICATION PROCESSING
  521
  522                ;        THE FOLLOWING CODE FIGURES OUT THE VOICE NUMBER THAT WILL BE
  523                ;        ASSUMED BY THE OBJECT CODE INTERPRETER WHEN THE MUSIC IS PLAYED
  524
  525 20CE A52D      STAT11:  LDA    EVTBLD       ; TEST IF THE START OF A NEW EVENT
  526 20D0 D016               BNE    STAT12       ; SKIP AHEAD IF NOT
  527 20D2 852F               STA    VPPNT        ; START SCAN WITH VOICE 1
  528 20D4 C620               DEC    EVTBLD       ; SET EVENT BEING BUILT FLAG
  529 20D6 A540               LDA    VXDUR        ; MAKE SURE AT LEAST ONE VOICE IS ACTIVE
  530 20D8 2541               AND    VXDUR+1      ; AND HAS A ZERO REMAINING DURATION
  531 20DA 2542               AND    VXDUR+2
  532 20DC 2543               AND    VXDUR+3
  533 20DE F008               BEQ    STAT12       ; SKIP AHEAD IF OK
  534 20E0 A915               LDA    #X'15        ; SIGNAL ER-15 IF NOT, EITHER NO VOICES
  535 20E2 20F925             JSR    ER           ; ACTIVE OR COMPILER ERROR
  536 20E5 4C221C             JMP    KIMMON       ; ABORT COMPILATION
  537
  538 20E8 A62F      STAT12:  LDX    VPPNT        ; GET CURRENT VOICE NUMBER
  539 20EA B540      STA122:  LDA    VXDUR,X      ; GET DURATION BYTE OF CURRENT VOICE
  540 20EC F003               BEQ    STA125       ; GO ASSIGN THIS NOTE SPEC TO IT IF ZERO
  541 20EE E8                 INX                 ; INCREMENT TO NEXT VOICE NUMBER
  542 20EF D0F9               BNE    STA122       ; GO TRY IT
  543
  544 20F1 A526      STA125:  LDA    NTVOIC       ; TEST IF A VOICE NUMBER GIVEN EXPLICITLY
  545 20F3 F00C               BEQ    STAT13       ; JUMP IF NOT
  546 20F5 C626               DEC    NTVOIC       ; CHANGE EXPLICIT VOICE NUMBER TO ZERO ORG
  547 20F7 8A                 TXA                 ; COMPARE WITH IMPLICIT VOICE NUMBER
  548 20F8 C526               CMP    NTVOIC
  549 20FA F005               BEQ    STAT13       ; JUMP AHEAD IF EQUAL, OK
  550 20FC A907               LDA    #7           ; SIGNAL ER 7 IF NOT EQUAL
  551 20FE 20F925             JSR    ER           ; AND CONTINUE USING ASSUMED VOICE NUMBER
  552
  553 2101 A529      STAT13:  LDA    NTPTC        ; TEST IF A REST WAS SPECIFIED
  554 2103 F056               BEQ    STA155       ; JUMP IF SO               .
  555 2105 A52A               LDA    NTOCT        ; TEST IF EXPLICIT OCTAVE NUMBER GIVEN
  556 2107 D002               BNE    STAT14       ; JUMP IF SO
  557 2109 B534               LDA    VXOCT,X      ; IF NOT, USE LAST USED OCTAVE NUMBER
  558 210B 9534      STAT14:  STA    VXOCT,X      ; UPDATE THE LAST USED OCTAVE NUMBER
  559 210D 0A                 ASLA                ; COMPUTE 12 TIMES THE OCTAVE NUMBER
  560 210E 0A                 ASLA
  561 210F 852A               STA    NTOCT
  562 2111 0A                 ASLA
  563 2112 652A               ADC    NTOCT
  564 2114 6529               ADC    NTPTC        ; ADD IN THE PITCH WITHIN THE OCTAVE
  565 2116 69F4               ADC    #-12         ; ADD IN CORRECTION FACTOR SO THAT C1=1
  566 2118 C93E               CMP    #62          ; TEST IF NOTE IS IN RANGE
  567 211A 9007               BCC    STA145       ; JUMP IF OK
  568 211C A908               LDA    #8           ; SIGNAL ER 8 IF OUT OF RANGE
  569 211E 20F925             JSR    ER
  570 2121 A93D               LDA    #61          ; AND USE HIGHEST POSSIBLE NOTE
  571 2123 8529      STA145:  STA    NTPTC        ; SAVE COMPOSITE PITCH IN NTPTC
  572 2125 B538               LDA    VXABS,X      ; TEST IF ABSOLUTE PITCH REQUIRED
  573 2127 D019               BNE    STAT15       ; JUMP IF SO
  574 2129 A529               LDA    NTPTC        ; TEST IF DIFFERENCE BETWEEN PREVIOUS PITCH
  575 212B 38                 SEC                 ; AND NEXT PITCH IS SMALL ENOUGH FOR A
  576 212C F530               SBC    VXPTC,X      ; SHORT NOTE SPECIFICATION
  577 212E C908               CMP    #8
  578 2130 1010               BPL    STAT15       ; JUMP IF GREATER THAN +7
  579 2132 C9F9               CMP    #-7
  580 2134 300C               BMI    STAT15       ; JUMP IF LESS THAN -7
  581
  582 2136 0A                 ASLA                ; FORMAT A SHORT NOTE SPECIFICATION
  583 2137 0A                 ASLA
  584 2138 0A                 ASLA
  585 2139 0A                 ASLA                ; PITCH DISPLACEMENT IN UPPER HEX DIGIT
  586 213A 0527               ORA    NTDUR        ; DURATION IN LOWER HEX DIGIT
  587 213C 208623             JSR    CODOU        ; OUTPUT A BYTE OF OBJECT CODE
  588 213F 4C6521             JMP    STAT16       ; GO CLEANUP AFTER NOTE SPECIFICATION
  589
  590 2142 A960      STAT15:  LDA    #X'60        ; FORMAT AN ABSOLUTE LONG NOTE SPEC
  591 2144 208623             JSR    CODOU        ; OUTPUT A 60 TO SIGNAL LONG NOTE SPEC
  592 2147 A529               LDA    NTPTC        ; GET COMPOSITE PITCH
  593 2149 0A                 ASLA                ; MULTIPLY IT BY 2
  594 214A 208623             JSR    CODOU        ; OUTPUT AS A BYTE OF OBJECT CODE
  595 214D B53C               LDA    VXWAV,X      ; GET WAVEFORM NUMBER FOR THE VOICE
  596 214F 0A                 ASLA                ; SHIFT LEFT 4 FOR LEFT HEX DIGIT
  597 2150 0A                 ASLA
  598 2151 0A                 ASLA
  599 2152 0A                 ASLA
  600 2153 0527               ORA    NTDUR        ; DURATION IN LOWER HEX DIGIT
  601 2155 208623             JSR    CODOU        ; OUTPUT AS A BYTE OF OBJECT CODE
  602 2158 4C6521             JMP    STAT16       ; GO CLEANUP AFTER NOTE SPEC
  603
  604 215B A980      STA155:  LDA    #X'80        ; FORMAT A REST SPEC
  605 215D 0527               ORA    NTDUR        ; OR IN DURATION
  606 215F 208623             JSR    CODOU        ; OUTPUT AS A BYTE OF OBJECT CODE
  607 2162 4C6921             JMP    STA165       ; SKIP PITCH UPDATE ON RESTS
  608
  609 2165 A529      STAT16:  LDA    NTPTC        ; CLEANUP AFTER NOTE SPEC
  610 2167 9530               STA    VXPTC,X      ; UPDATE CURRENT PITCH FOR THE VOICE
  611 2169 A900      STA165:  LDA    #0           ; CLEAR FORCE ABSOLUTE PITCH FLAG
  612 216B 9538               STA    VXABS,X
  613 216D A528               LDA    NTDURX       ; UPDATE DURATION FOR THE VOICE
  614 216F 9540               STA    VXDUR,X
  615
  616                ;        DETERMINE IF A COMPLETE MUSICAL EVENT HAS BEEN BUILT, IF NOT
  617                ;        LEAVE VPPNT POINTING TO NEXT EXPIRED VOICE. IF SO, CLOSE OUT
  618                ;        THIS EVENT BY SUBTRACTING SHORTEST DURATION IN THIS EVENT
  619                ;        FROM THE DURATION OF EACH ACTIVE VOICE AND TURNING THE EVENT
  620                ;        BEING BUILT FLAG OFF
  621
  622 2171 E8        STAT17:  INX                 ; INCREMENT TO NEXT VOICE
  623 2172 E004               CPX    #4           ; TEST IF ALL VOICES SCANNED
  624 2174 F008               BEQ    STAT18       ; JUMP IF SO
  625 2176 B540               LDA    VXDUR,X      ; TEST IF THIS NEXT VOICE WILL REQUIRE A
  626                                             ; NOTE SPEC
  627 2178 D0F7               BNE    STAT17       ; GO TO NEXT VOICE IF NOT
  628 217A 862F               STX    VPPNT        ; SAVE THE NEXT VOICE NUMBER
  629 217C F030               BEQ    ESPEC        ; GO TO END OF SPEC PROCESSING
  630
  631 217E A9FF      STAT18:  LDA    #X'FF        ; SCAN THE VOICES TO FIND THE ONE WITH THE
  632 2180 D53F      STAT19:  CMP    VXDUR-1,X    ; SHORTEST DURATION
  633 2182 9002               BCC    STAT20
  634 2184 B53F               LDA    VXDUR-1,X    ; UPDATE CURRENT SHORTEST IN A
  635 2186 CA        STAT20:  DEX
  636 2187 D0F7               BNE    STAT19       ; LOOP UNTIL ALL 4 VOICES SCANNED
  637
  638 2189 852E               STA    DUR          ; SCAN THE VOICES AGAIN AND SUBTRACT THE
  639 218B B540      STAT21:  LDA    VXDUR,X      ; SHORTEST DURATION FROM EACH ONE THAT IS
  640 218D C9FF               CMP    #X'FF        ; ACTIVE
  641 218F F005               BEQ    STAT22       ; SKIP INACTIVE VOICES
  642 2191 38                 SEC
  643 2192 E52E               SBC    DUR
  644 2194 9540               STA    VXDUR,X
  645 2196 E8        STAT22:  INX
  646 2197 E004               CPX    #4
  647 2199 D0F0               BNE    STAT21
  648
  649 219B A900               LDA    #0           ; CLEAR THE EVENT BEING BUILT FLAG SINCE AN
  650 219D 852D               STA    EVTBLD       ; EVENT HAS JUST BEEN COMPILED
  651 219F F00D               BEQ    ESPEC        ; GO TO END OF SPEC PROCESSING
  652
  653                ;        CLEANUP AFTER A KEYWORD OR NOTE SPECIFICATION
  654
  655 21A1 A52D      EXSPEC:  LDA    EVTBLD       ; TEST IF AN EXECUTABLE KEYWORD
  656                                             ; SPECIFICATION PROCESSED IN THE MIDDLE OF
  657                                             ; A MUSICAL EVENT
  658 21A3 F009               BEQ    ESPEC        ; OK IF NOT; GO TO END OF SPEC PROCESSING
  659 21A5 A910               LDA    #X'10        ; GENERATE ER-10 IF SO
  660 21A7 20F925             JSR    ER
  661 21AA A900               LDA    #0           ; EMULATE INTERPRETER ACTION IN THIS
  662 21AC 852D               STA    EVTBLD       ; SITUATION BY ABORTING CURRENT EVENT AND
  663                                             ; STARTING A NEW ONE
  664
  665 21AE A417      ESPEC:   LDY    INBFPT       ; SCAN FOR A SEMICOLON OR CR
  666 21B0 B100      ESPEC1:  LDA    (INBFA),Y    ; GET CURRENT CHARACTER FROM INPUT BUFFER
  667 21B2 C8                 INY                 ; MOVE TO NEXT CHARACTER IN INPUT BUFFER
  668 21B3 C90D               CMP    #X'0D        ; TEST IF ACR
  669 21B5 F00B               BEQ    ESTAT        ; GO TO END OF STATEMENT CLEANUP IF SO
  670 21B7 C93B               CMP    #';'         ; TEST IF A ";"
  671 21B9 F002               BEQ    ESPEC2       ; SKIP AHEAD IF SO
  672 21BB D0F3               BNE    ESPEC1       ; GO TEST NEXT CHARACTER IF NOT
  673 21BD 8417      ESPEC2:  STY    INBFPT       ; UPDATE INPUT BUFFER POINTER
  674 21BF 4CAC20             JMP    STAT8        ; GO PROCESS CONTINUATION OF STATEMENT
  675
  676                ;        CLEANUP AT END OF INPUT LINE
  677
  678 21C2 A90D      ESTAT:   LDA    #X'0D        ; END LISTING LINE WITH A CR AND LF
  679 21C4 205D23             JSR    OUCH
  680 21C7 A90A               LDA    #X'0A
  681 21C9 205D23             JSR    OUCH
  682 21CC A515               LDA    LIST         ; TEST IF LISTING ENABLED
  683 21CE F003               BEQ    ESTAT1       ; SKIP PRINT IF NOT
  684 21D0 20F025             JSR    OU           ; PRINT THE LISTING LINE IF LISTING ENABLED
  685 21D3 A523      ESTAT1:  LDA    ENDFLG       ; TEST IF END STATEMENT JUST PROCESSED
  686 21D5 D003               BNE    ESTAT2
  687 21D7 4C3720             JMP    STAT         ; GO PROCESS NEXT STATEMENT IF NOT
  688 21DA 4C221C    ESTAT2:  JMP    KIMMON       ; GO RETURN TO KIM MONITOR IF SO
  689
                              .PAGE  'KEYWORD PROCESSOR ROUTINES'
  690                ;        NVCP DEFINE NUMBER OF VOICES, 1 ARGUMENT, RANGE 1-4
  691
  692 21DD 20D225    NVCP:    JSR    NNB          ; MOVE TO NEXT NON-BLANK CHARACTER
  693 21E0 202123             JSR    GETARG       ; PROCESS THE NUMBER THAT IS NEXT EXPECTED
  694                                             ; AND TEST IT FOR OVERFLOW ERRORS
  695 21E3 A522               LDA    NBF          ; TEST FOR LEGITIMATE RANGE
  696 21E5 F06E               BEQ    ARGER        ; ZERO NOT ALLOWED
  697 21E7 C905               CMP    #5           ; GREATER THAN 4 NOT ALLOWED
  698 21E9 B06A               BCS    ARGER
  699 21EB A950               LDA    #X'50        ; FORMAT A SET NUMBER OF VOICES SPEC
  700 21ED 208623             JSR    CODOU
  701 21F0 A522               LDA    NBF
  702 21F2 208623             JSR    CODOU
  703 21F5 4CA121             JMP    EXSPEC       ; GO TO END OF EXECUTABLE SPEC PROCESSING
  704
  705                ;        ACTP ACTIVATE VOICES, MULTIPLE ARGS ALLOWED, RANGE 1-4
  706
  707 21F8 A990      ACTP:    LDA    #X'90        ; SET ACTIVATE VOICE CODE
  708 21FA D002               BNE    CTP
  709
  710                ;        DCTP DEACTIVATE VOICES, MULTIPLE ARGS ALLOWED, RANGE 1-4
  711
  712 21FC A980      DCTP:    LDA    #X'80        ; SET DEACTIVATE VOICE CODE
  713
  714                ;        COMMON CODE FOR ACTIVATE AND DEACTIVATE VOICE KEYWORDS
  715
  716 21FE 8546      CTP:     STA    CDSAV        ; SAVE ACTIVATE/DEACTIVATE CODE
  717 2200 202123    CTP1:    JSR    GETARG       ; PROCESS THE NUMBER THAT IS NEXT EXPECTED
  718                                             ; AND TEST IT FOR OVERFLOW ERRORS
  719 2203 C622               DEC    NBF          ; DECREMENT VOICE # FOR ZERO ORIGIN IN CODE
  720 2205 A522               LDA    NBF          ; TEST FOR LEGITIMATE RANGE
  721 2207 C904               CMP    #4           ; GREATER THAN 4 (BEFORE DECR) NOT ALLOWED
  722 2209 B04A               BCS    ARGER
  723 220B A546               LDA    CDSAV        ; FORMAT AN ACTIVATE/DEACTIVATE VOICE SPEC
  724 220D 208623             JSR    CODOU
  725 2210 A522               LDA    NBF
  726 2212 208623             JSR    CODOU
  727 2215 A622               LDX    NBF          ; GET VOICE NUMBER BACK
  728 2217 A546               LDA    CDSAV        ; GET ACTIVATE/DEACTIVATE CODE BACK
  729 2219 4990               EOR    #X'90        ; FORM FF IF DCT, 00 IF ACT
  730 221B F002               BEQ    CTP2
  731 221D A9FF               LDA    #X'FF
  732 221F 9540      CTP2:    STA    VXDUR,X      ; UPDATE ACTIVE/INACTIVE STATUS OF VOICE
  733 2221 A417               LDY    INBFPT       ; TEST IF ARGUMENT TERMINATOR WAS A ,
  734 2223 B100               LDA    (INBFA),Y
  735 2225 C92C               CMP    #','
  736 2227 F003               BEQ    CTP3         ; SKIP AHEAD IF SO
  737 2229 4CA121             JMP    EXSPEC       ; GO TO END OF SPEC PROCESSING IF NOT
  738 222C E617      CTP3:    INC    INBFPT       ; SKIP THE COMMA
  739 222E 4C0022             JMP    CTP1         ; AND GO PROCESS NEXT ARGUMENT
  740
  741                ;        WAVP ASSIGN WAVEFORM TO VOICE, 2 ARGS, FIRST IS WAVEFORM
  742                ;        NUMBER RANGE 1-16, SECOND IS VOICE NUMBER, RANGE 1-4
  743
  744 2231 202123    WAVP:    JSR    GETARG       ; PROCESS THE NUMBER THAT IS NEXT EXPECTED
  745                                             ; AND TEST IT FOR OVERFLOW ERRORS
  746 2234 C622               DEC    NBF          ; CONVERT WAVE # TO ZERO ORIGIN
  747 2236 A522               LDA    NBF          ; TEST FOR LEGITIMATE RANGE FOR WAVE NUMBER
  748 2238 C910               CMP    #16          ; GREATER THAN 16 (BEFORE DECR) NOT ALLOWED
  749 223A B019               BCS    ARGER
  750 223C 48                 PHA                 ; SAVE WAVE NUMBER IF VALID
  751 223D E617               INC    INBFPT       ; SKIP OVER COMMA ARGUMENT SEPARATOR
  752 223F 202123             JSR    GETARG       ; PROCESS THE EXPECTED VOICE NUMBER
  753 2242 C622               DEC    NBF          ; CONVERT VOICE # TO ZERO ORIGIN
  754 2244 A522               LDA    NBF          ; TEST FOR LEGITIMATE RANGE FOR VOICE NO.
  755 2246 C904               CMP    #4
  756 2248 B00B               BCS    ARGER
  757 224A AA                 TAX                 ; PUT VOICE NUMBER IN X
  758 224B 68                 PLA                 ; GET WAVEFORM NUMBER BACK
  759 224C 953C               STA    VXWAV,X      ; UPDATE CURRENT WAVEFORM NUMBER IN VOICE
  760 224E A9FF               LDA    #X'FF
  761 2250 9538               STA    VXABS,X      ; SET FORCE ABSOLUTE PITCH SO THAT
  762                                             ; NEXT NOTE GENERATED FOR THE VOICE WILL
  763                                             ; UPDATE THE WAVEFORM NUMBER IN THE
  764                                             ; INTERPRETER
  765 2252 4CAE21             JMP    ESPEC        ; GO TO END OF SPEC PROCESSING
  766
  767 2255 A901      ARGER:   LDA    #1           ; COMMON ARGUMENT ERROR ROUTINE
  768 2257 20F925             JSR    ER
  769 225A 4CAE21             JMP    ESPEC        ; GO TO END OF SPEC PROCESSING
  770
  771                ;        JSRP JUMP TO MUSICAL SUBROUTINE, 1 ARGUMENT, RANGE 1-255
  772
  773 225D A920      JSRP:    LDA    #X'20        ; SET JSR CODE
  774 225F D002               BNE    JP           ; GO TO COMMON JMP/JSR CODE
  775
  776                ;        JMP    UNCONDITIONAL JUMP, 1 ARGUMENT, RANGE 1-255
  777
  778 2261 A940      JMPP:    LDA    #X'40        ; SET JMP CODE
  779
  780                ;        COMMON JMP AND JSR CODE
  781
  782 2263 8546      JP:      STA    CDSAV        ; SAVE JSR/JMP CODE
  783 2265 202123             JSR    GETARG       ; EVALUATE ARGUMENT WHICH IS A NUMERICAL
  784 2268 A522               LDA    NBF          ; SYMBOL
  785 226A 206625             JSR    SYMSR        ; SEARCH SYMBOL TABLE FOR SYMBOL
  786 226D 901C               BCC    JP1          ; JUMP IF SYMBOL NOT FOUND
  787 226F A546               LDA    CDSAV        ; GENERATE JSR/JMP SPEC IN OBJECT CODE
  788 2271 208623             JSR    CODOU
  789 2274 A000               LDY    #0           ; GET LOW BYTE OF SYMBOL VALUE
  790 2276 B11D               LDA    (SYMTPT),Y
  791 2278 38                 SEC                 ; SUBTRACT CODE ORIGIN FROM IT FOR RELATIVE
  792 2279 E506               SBC    CODEA        ; ADDRESSING WITHIN OBJECT CODE
  793 227B 08                 PHP                 ; SAVE CARRY OUT STATUS
  794 227C 208623             JSR    CODOU        ; PUT LOW BYTE IN OBJECT CODE
  795 227F C8                 INY                 ; GET HIGH BYTE OF SYMBOL VALUE
  796 2280 B11D               LDA    (SYMTPT),Y
  797 2282 28                 PLP                 ; RESTORE CARRY FROM LOW BYTE
  798 2283 E507               SBC    CODEA+1
  799 2285 208623             JSR    CODOU        ; PUT HIGH BYTE IN OBJECT CODE
  800 2288 4CAE21             JMP    ESPEC        ; GO TO END OF SPEC PROCESSING
  801 228B A902      JP1:     LDA    #2           ; GENERATE UNDEFINED SYMBOL ERROR
  802 228D 20F925             JSR    ER
  803 2290 4CA121             JMP    EXSPEC       ; GO TO END OF EXECUTABLE SPEC PROCESSING
  804
  805                ;        RTSP   RETURN FROM SUBROUTINE, NO ARGS
  806
  807 2293 A930      RTSP:    LDA    #X'30        ; GENERATE A RTS SPEC IN THE OBJECT CODE
  808 2295 208623             JSR    CODOU
  809 2298 4CA121             JMP    EXSPEC       ; GO TO END OF EXECUTABLE SPEC PROCESSING
  810
  811                ;        ENDP   END OF SONG, NO ARGS
  812
  813 229B A900      ENDP:    LDA    #0           ; GENERATE AN END SPEC IN THE OBJECT CODE
  814 229D 208623             JSR    CODOU
  815 22A0 A9FF               LDA    #X'FF        ; SET END FLAG
  816 22A2 8523               STA    ENDFLG
  817 22A4 A524               LDA    SUBSKP       ; TEST IF HANGING SUB LEFT OVER
  818 22A6 0525               ORA    SUBSKP+1
  819 22A8 F005               BEQ    ENDP1        ; SKIP AHEAD IF OK
  820 22AA A914               LDA    #X'14        ; FORMAT ER-14 IF SO
  821 22AC 201300             JSR    ERR
  822 22AF 4CAE21    ENDP1:   JMP    ESPEC        ; GO TO END OF SPEC PROCESSING
  823
  824                ;        TPOP   SET TEMPO, 1 ARGUMENT
  825
  826 22B2 202123    TPOP:    JSR    GETARG       ; PROCESS THE NUMBER THAT IS NEXT EXPECTED
  827                                             ; AND TEST IT FOR OVERFLOW ERRORS
  828 22B5 D09E               BNE    ARGER        ; JUMP OUT IF OVERFLOW ERROR
  829 22B7 A522               LDA    NBF          ; TEST FOR LEGITIMATE RANGE
  830 22B9 F09A               BEQ    ARGER        ; ZERO NOT ALLOWED
  831 22BB A910               LDA    #X'10        ; GENERATE A CHANGE TEMPO SPEC IN CODE
  832 22BD 208623             JSR    CODOU
  833 22C0 A522               LDA    NBF          ; GET THE TEMPO ARGUMENT
  834 22C2 208623             JSR    CODOU        ; *** USE IN RAW FORM *** TEMPORARY
  835 22C5 4CA121             JMP    EXSPEC       ; GO TO END OF EXECUTABLE SPEC PROCESSING
  836
  837                ;        ABSP   FORCE ABSOLUTE PITCH UPDATE OF ALL VOICES, NO ARGS
  838
  839 22C8 A204      ABSP:    LDX    #4           ; SET UP TO SET FORCE ABSOLUTE PITCH FLAG
  840 22CA A9FF               LDA    #X'FF        ; IN ALL 4 VOICES
  841 22CC 9537      ABSP1:   STA    VXABS-1,X
  842 22CE CA                 DEX
  843 22CF D0FB               BNE    ABSP1
  844 22D1 4CAE21             JMP    ESPEC        ; GO TO END OF SPEC PROCESSING
  845
  846                ;        SUBP   DEFINE BEGINNING OF SUBROUTINE AREA
  847
  848 22D4 A524      SUBP:    LDA    SUBSKP       ; TEST IF ALREADY A PENDING SUB
  849 22D6 0525               ORA    SUBSKP+1
  850 22D8 D018               BNE    SUBP1        ; GO TO ERROR IF SO
  851 22DA A940               LDA    #X'40        ; FORMAT AN UNCONDITIONAL JUMP IN THE
  852 22DC 208623             JSR    CODOU        ; OBJECT CODE
  853 22DF A519               LDA    CODEPT       ; SAVE PRESENT OBJECT CODE ADDRESS
  854 22E1 8524               STA    SUBSKP       ; IN SUBSKP
  855 22E3 A51A               LDA    CODEPT+1
  856 22E5 8525               STA    SUBSKP+1
  857 22E7 A900               LDA    #0           ; PUT BLANK ADDRESS FIELD IN THE JUMP
  858 22E9 208623             JSR    CODOU
  859 22EC 208623             JSR    CODOU
  860 22EF 4CA121             JMP    EXSPEC       ; GO TO END OF EXECUTABLE SPEC PROCESSING
  861 22F2 A912      SUBP1:   LDA    #X'12        ; GENERATE ER-13 IF ATTEMPT TO NEST SUB
  862 22F4 201300             JSR    ERR
  863 22F7 4CA121             JMP    EXSPEC       ; DO NOTHING
  864
  865                ;        ESBP   DEFINE END OF SUBROUTINE AREA
  866
  867 22FA A524      ESBP:    LDA    SUBSKP       ; TEST IF A PREVIOUS MATCHING "SUB"
  868 22FC 0525               ORA    SUBSKP+1
  869 22FE F019               BEQ    ESBP1        ; JUMP TO ERROR IF NOT
  870 2300 A000               LDY    #0
  871 2302 38                 SEC                 ; SET CARRY FOR SUBTRACTION
  872 2303 A519               LDA    CODEPT       ; COMPUTE RELATIVE ADDRESS IN OBJECT CODE
  873 2305 E506               SBC    CODEA        ; AND FILL IN ADDRESS FIELD OF JUMP
  874 2307 9124               STA    (SUBSKP),Y   ; GENERATED BY "SUB" STATEMENT
  875 2309 C8                 INY
  876 230A A51A               LDA    CODEPT+1
  877 230C E507               SBC    CODEA+1
  878 230E 9124               STA    (SUBSKP),Y
  879 2310 A900               LDA    #0           ; CLEAR SUB DEFINITION
  880 2312 8524               STA    SUBSKP
  881 2314 8525               STA    SUBSKP+1
  882 2316 4CA121             JMP    EXSPEC       ; GO TO END OF EXECUTABLE SPEC PROCESSING
  883 2319 A913      ESBP1:   LDA    #X'13        ; GENERATE ER-12 IF ESB WITHOUT SUB
  884 231B 201300             JSR    ERR
  885 231E 4CA121             JMP    EXSPEC       ; DO NOTHING
  886
  887                ;        GETARG GET ARGUMENT, CHECK FOR OVERFLOW, A=0 IF OK, NOT ZERO
  888                ;        IF OVEFFLOW ERROR
  889
  890 2321 20D225    GETARG:  JSR    NNB          ; FIRST MOVE TO NEXT NON-BLANK CHARACTER
  891 2324 20B724             JSR    NCOL         ; COLLECT WHAT IS ASSUMED TO BE AN ARGUMENT
  892 2327 A52C               LDA    NMERR        ; GET ERROR FLAG FROM NCOL
  893 2329 60                 RTS                 ; RETURN WITH ERROR CODE IN A
  894
                              .PAGE  'MISCELLANEOUS LITTLE OUTPUT ROUTINES'
  895
  896                ;        OULOC  OUTPUT LOCATION PLACE IN LISTING THE CURRENT VALUE OF
  897                ;               THE OBJECT CODE POINTER AS 4 DIGIT HEX FOLLOWED BY 2
  898                ;               BLANKS
  899                ;               NO REGISTERS BOTHERED
  900
  901 232A 48        OULOC:   PHA                 ; SAVE A
  902 232B A51A               LDA    CODEPT+1     ; OUTPUT UPPER BYTE FIRST FOR NATURAL ORDER
  903 232D 203F23             JSR    OUHX
  904 2330 A519               LDA    CODEPT       ; OUTPUT LOWER BYTE
  905 2332 203F23             JSR    OUHX
  906 2335 A920               LDA    #' '         ; FOLLOW WITH 2 BLANKS
  907 2337 205D23             JSR    OUCH
  908 233A 205D23             JSR    OUCH
  909 233D 68                 PLA                 ; RESTORE A
  910 233E 60                 RTS                 ; RETURN
  911
  912
  913                ;        OUHX   OUTPUT CONTENTS OF A AS 2 HEX DIGITS
  914                ;        NO REGISTERS BOTHERED
  915
  916 233F 48        OUHX:    PHA                 ; SAVE A
  917 2340 4A                 LSRA                ; RIGHT JUSTIFY HIGH 4 BITS
  918 2341 4A                 LSRA
  919 2342 4A                 LSRA
  920 2343 4A                 LSRA
  921 2344 205023             JSR    OUHX1        ; OUTPUT AS HEX DIGIT
  922 2347 68                 PLA                 ; RESTORE A
  923 2348 48                 PHA                 ; BUT KEEP IT ON THE STACK
  924 2349 290F               AND    #X'0F        ; ISOLATE LOW 4 BITS AND OUTPUT AS HEX
  925 234B 205023             JSR    OUHX1
  926 234E 68                 PLA                 ; RESTORE A
  927 234F 60                 RTS                 ; RETURN
  928
  929 2350 18        OUHX1:   CLC                 ; CONVERT 4 BIT NUMBER IN A TO ASCII HEX
  930 2351 6930               ADC    #'0'         ; DIGIT
  931 2353 C93A               CMP    #'9'+1
  932 2355 3002               BMI    OUHX2
  933 2357 6906               ADC    #'A'-'0'-X'A-1
  934 2359 206D23    OUHX2:   JSR    OUCH         ; OUTPUT THE ASCII HEX DIGIT
  935 235C 60                 RTS                 ; RETURN
  936
  937                ;        OUCH   OUTPUT ASCII CHARACTER IN A WITH AUTOMATIC NEW LINE IF
  938                ;               LINE IS TOO LONG AND LISTING ENABLED
  939                ;               NO REGISTERS BOTHERED
  940
  941 235D 48        OUCH:    PHA                 ; SAVE CHARACTER TO OUTPUT
  942 235E 8A                 TXA                 ; AND INDEX X
  943 235F 48                 PHA
  944 2360 98                 TYA                 ; AND INDEX Y
  945 2361 48                 PHA
  946 2362 A418               LDY    OUBFPT       ; TEST IF 72 CHARACTERS ALREADY IN OUTPUT
  947 2364 C048               CPY    #72          ; BUFFER
  948 2366 9010               BCC    OUCH1        ; JUMP AHEAD IF LESS THAN 72
  949 2368 A900               LDA    #X'00        ; PUT A CARRIAGE RETURN AND LINE FEED IN
  950 236A 9102               STA    (OUBFA),Y    ; THE OUTPUT BUFFER
  951 236C C8                 INY
  952 236D A90A               LDA    #X'0A
  953 236F 9102               STA    (OUBFA),Y
  954 2371 20F025             JSR    OU           ; PRINT THE CONTENTS OF THE OUTPUT BUFFER
  955 2374 A000               LDY    #0           ; REINITIALIZE OUTPUT BUFFER POINTER
  956 2376 8418               STY    OUBFPT
  957 2378 BA        OUCH1:   TSX                 ; RESTORE CHARACTER TO BE OUTPUT
  958 2379 BD0301             LDA    X'103,X
  959 237C 9102               STA    (OUBFA),Y    ; PUT IT IN THE OUTPUT BUFFER
  960 237E E618               INC    OUBFPT       ; INCREMENT OUTPUT BUFFER POINTER
  961 2380 68                 PLA                 ; RESTORE REGISTERS
  962 2381 A8                 TAY
  963 2382 68                 PLA
  964 2383 AA                 TAX
  965 2384 68                 PLA
  966 2385 60                 RTS                 ; RETURN
  967
                              .PAGE  'OBJECT CODE OUTPUT ROUTINE'
  968                ;        CODOU  CODE OUTPUT ADDS A BYTE OF OBJECT CODE TO THE OBJECT
  969                ;               CODE AREA IN MEMORY AND ALSO CONVERTS IT TO HEX AND ADDS
  970                ;               TWO HEX DIGITS FOLLOWED BY A BLANK TO THE LISTING OUTPUT
  971                ;               BUFFER.  GENERATES AN ERROR MESSAGE IF THE OBJECT CODE
  972                ;               AREA OVERFLOWS
  973                ;               ENTER WITH CODE BYTE IN A
  974                ;               PRESERVES ALL REGISTERS
  975                ;               ON CODE OVERFLOW ERROR, A NORMAL RETURN IS TAKEN SO ONCE
  976                ;               GENERATED, THE ERROR WILL REPEAT UNTIL THE COMPILATION
  977                ;               IS INTERRUPTED
  978                ;               NO REGISTERS BOTHERED
  979
  980 2386 48        CODOU:   PHA                 ; SAVE REGISTERS
  981 2387 8A                 TXA
  982 2388 48                 PHA
  983 2389 98                 TYA
  984 238A 48                 PHA
  985 238B A509               LDA    CODELN+1     ; TEST IF OBJECT CODE AREA IS FULL
  986 238D C51C               CMP    CODECT+1
  987 238F D00E               BNE    CODOU1       ; JUMP IF NOT
  988 2391 A508               LDA    CODELN
  989 2393 C51B               CMP    CODECT
  990 2395 D008               BNE    CODOU1       ; JUMP IF NOT
  991 2397 A905               LDA    #5           ; GENERATE ER 5 IF FULL
  992 2399 20F925             JSR    ER
  993 239C 4CB923             JMP    CODOU2       ; GO RESTORE REGISTERS AND RETURN
  994
  995 239F BA        CODOU1:  TSX                 ; GET THE CODE BYTE BACK
  996 23A0 BD0301             LDA    X'103,X
  997 23A3 A000               LDY    #0           ; STORE CODE BYTE IN OBJECT CODE AREA
  998 23A5 9119               STA    (CODEPT),Y
  999 23A7 203F23             JSR    OUHX         ; PRINT THE CODE BYTE IN HEX FOLLOWED BY A
 1000 23AA A920               LDA    #' '         ; BLANK
 1001 23AC 205D23             JSR    OUCH
 1002 23AF A219               LDX    #CODEPT      ; INCREMENT OBJECT CODE POINTER
 1003 23B1 20E625             JSR    DINC
 1004 23B4 A21B               LDX    #CODECT      ; INCREMENT OBJECT CODE BYTE COUNT
 1005 23B6 20E625             JSR    DINC
 1006 23B9 68        CODOU2:  PLA                 ; RESTORE REGISTERS
 1007 23BA A8                 TAY
 1008 23BB 68                 PLA
 1009 23BC AA                 TAX
 1010 23BD 68                 PLA
 1011 23BE 60                 RTS                 ; RETURN
 1012
                              .PAGE  'NOTE COLLECT ROUTINE'
 1013                ;        NTCOL  NOTE COLLECT ROUTINE
 1014                ;               ENTER WITH INBFPT POINTING TO WHAT IS BELIEVED TO BE A
 1015                ;               NOTE SPECIFICATION
 1016                ;               EXIT IWTH THE FOLLOWING BASE PAGE VARIABLES SET:
 1017                ;               NTERR - NTCOL ERROR CODE, O = NO ERROR
 1018                ;               NTVOIC - VOICE NUMBER, ZERO IF NOT EXPLICITLY SPECIFIED
 1019                ;               NTDUR - NOTE DURATION, 1-15 AS IN OBJECT CODE
 1020                ;               NTPTC - NOTE PITCH, 0-12, C=1, B=12, REST=0
 1021                ;               NTOCT - NOTE OCTAVE, 1-6, 0 IF NOT SPECIFIED
 1022                ;               INBFPT POINTING TO CHARACTER FOLLOWING NOTE
 1023                ;               SPECIFICATION, EITHER BLANK, ;, OR CR FOR NO ERROR
 1024                ;               X AND Y REGISTERS PRESERVED
 1025
 1026 23BF 8A        NTCOL:   TXA                 ; SAVE X AND Y REGISTERS
 1027 23C0 48                 PHA
 1028 23C1 98                 TYA
 1029 23C2 48                 PHA
 1030 23C3 A900               LDA    #0           ; INITIALIZE SOME RETURN ARGUMENTS
 1031 23C5 8528               STA    NTERR
 1032 23C7 8526               STA    NTVOIC
 1033 23C9 852A               STA    NTOCT
 1034 23CB 8529               STA    NTPTC
 1035 23CD A417               LDY    INBFPT
 1036 23CF B100               LDA    (INBFA),Y    ; GET CURRENT CHARACTER
 1037 23D1 C935               CMP    #'4'+1       ; TEST IF A VALID VOICE # DIGIT
 1038 23D3 B009               BCS    NTCOL1       ; JUMP IF NOT
 1039 23D5 C931               CMP    #'1'
 1040 23D7 9005               BCC    NTCOL1       ; JUMP IF NOT
 1041 23D9 E930               SBC    #'0'         ; IF A VALID VOICE # DIGIT, STORE IN BINARY
 1042 23DB 8526               STA    NTVOIC       ; AT NTVOIC
 1043 23DD C8                 INY                 ; SKIP OVER THE VOICE NUMBER DIGIT
 1044 23DE B100      NTCOL1:  LDA    (INBFA),Y    ; TEST IF VALID NOTE LETTER
 1045 23E0 C948               CMP    #'G'+1
 1046 23E2 B038               BCS    NTCOL5       ; JUMP IF NOT
 1047 23E4 C941               CMP    #'A'
 1048 23E6 9034               BCC    NTCOL5       ; JUMP IF NOT
 1049 23E8 E940               SBC    #'A'-1       ; CONVERT NOTE LETTER INTO A NUMBER; A=1,
 1050 23EA 8529               STA    NTPTC        ; G=7 AND MULTIPLY IT BY 3
 1051 23EC 0A                 ASLA
 1052 23ED 6529               ADC    NTPTC
 1053 23EF 8529               STA    NTPTC        ; PLACE IT IN NTPTC UNTIL SHARPS AND FLATS
 1054                                             ; EVALUATED
 1055 23F1 C8                 INY                 ; SKIP OVER THE NOTE LETTER
 1056 23F2 B100               LDA    (INBFA),Y    ; GET NEXT CHARACTER
 1057 23F4 C923               CMP    #'#'         ; TEST IF A SHARP
 1058 23F6 D004               BNE    NTCOL2       ; JUMP IF NOT
 1059 23F8 E629               INC    NTPTC        ; INCREMENT NTPTC IF SHARP
 1060 23FA D006               BNE    NTCOL3
 1061 23FC C940      NTCOL2:  CMP    #'@'         ; TEST IF FLAT
 1062 23FE D003               BNE    NTCOL4       ; JUMP IF NOT
 1063 2400 C629               DEC    NTPTC        ; DECREMENT NTPTC IF FLAT
 1064 2402 C8        NTCOL3:  INY                 ; SKIP OVER # OR @ IF PRESENT
 1065 2403 A629      NTCOL4:  LDX    NTPTC        ; CONVERT VALUE IN NTPTC INTO FINAL FORM
 1066 2405 BD7824             LDA    NTPTCT-2,X
 1067 2408 8529               STA    NTPTC
 1068 240A B100               LDA    (INBFA),Y    ; TEST IF VALID OCTAVE DIGIT PRESENT
 1069 240C C937               CMP    #'6'+1
 1070 240E B018               BCS    NTCOL7       ; JUMP IF NOT
 1071 2410 C931               CMP    #'1'
 1072 2412 9014               BCC    NTCOL7       ; JUMP IF NOT
 1073 2414 E930               SBC    #'0'         ; CONVERT THE DIGIT TO BINARY AND PLACE IN
 1074 2416 852A               STA    NTOCT        ; NTOCT
 1075 2418 C8                 INY                 ; SKIP OVER THE OCTAVE DIGIT
 1076 2419 4C2824             JMP    NTCOL7       ; GO TO DURATION INTERPRET
 1077 241C C952      NTCOL5:  CMP    #'R'         ; TEST IF REST SPECIFICATION
 1078 241F F007               BEQ    NTCOL6       ; JUMP IF SO
 1079 2420 A906      NTCOL0:  LDA    #6           ; ER 6 IF NOT
 1080 2422 8528               STA    NTERR
 1081 2424 4C6D24             JMP    NTCOLD       ; GO RETURN
 1082 2427 C8        NTCOL6:  INY                 ; SKIP OVER THE R IF PRESENT, NTPTC WILL BE
 1083                                             ; ZERO SIGNIFYING A REST
 1084
 1085 2428 A201      NTCOL7:  LDX    #1           ; SCAN NTDURT TO DETERMINE IF CURRENT CHAR.
 1086 242A BD8E24    NTCOL8:  LDA    NTDURT-1,X   ; IS A VAILD DURATION
 1087 242D F045               BEQ    NTCOLE       ; JUMP OUT IF END OF TABLE, ILLEGAL DUR.
 1088 242F D100               CMP    (INBFA),Y
 1089 2431 F003               BEQ    NTCOL9       ; JUMP OUT IF MATCH
 1090 2433 E8                 INX                 ; INDEX TO NEXT TABLE ENTRY
 1091 2434 D0F4               BNE    NTCOL8       ; LOOP TO TRY NEXT ENTRY
 1092 2436 8A        NTCOL9:  TXA                 ; GET 3 TIMES X AND PUT IN NTDUR
 1093 2437 8527               STA    NTDUR
 1094 2439 0A                 ASLA
 1095 243A 6527               ADC    NTDUR
 1096 243C 8527               STA    NTDUR
 1097 243E C8                 INY                 ; SKIP OVER THE DURATION LETTER
 1098 243F B100               LDA    (INBFA),Y    ; GET CURRENT CHARACTER
 1099 2441 C92E               CMP    #'.'         ; TEST IF DURATION MODIFIER "."
 1100 2443 D004               BNE    NTCOLA       ; JUMP IF NOT
 1101 2445 C627               DEC    NTDUR        ; DECREMENT NTDUR IF A .
 1102 2447 D006               BNE    NTCOLB
 1103 2449 C933      NTCOLA:  CMP    #'3'         ; TEST IF DURATION MODIFIER "3" (TRIPLET)
 1104 244B D003               BNE    NTCOLC       ; JUMP IF NOT
 1105 244D E627               INC    NTDUR        ; INCREMENT NTDUR IF A 3
 1106 244F C8        NTCOLB:  INY                 ; SKIP OVER THE "." OR "3" IF PRESENT
 1107
 1108 2450 A627      NTCOLC:  LDX    NTDUR        ; CONVERT NTDUR INTO FINAL OBJECT FORM
 1109 2452 BD9424             LDA    NTDRT1-2,X
 1110 2455 F01D               BEQ    NTCOLE       ; JUMP IF ILLEGAL DURATION
 1111 2457 8527               STA    NTDUR
 1112 2459 AA                 TAX                 ; CONVERT NTDUR TO ABSOLUTE TIME VALUE FOR
 1113 245A BDA724             LDA    NTDRT2-1,X   ; COMPILER
 1114 2450 8528               STA    NTDURX
 1115 245F B100               LDA    (INBFA),Y    ; TEST IF VALID NOTE SPEC TERMINATOR
 1116 2461 C920               CMP    #' '         ; BLANK
 1117 2463 F008               BEQ    NTCOLD
 1118 2465 C93B               CMP    #';'         ; SEMICOLON
 1119 2467 F004               BEQ    NTCOLD
 1120 2469 C90D               CMP    #X'0D        ; CARRIAGE RETURN
 1121 2468 D0B3               BNE    NTCOL0       ; SIGNAL ER 6 IF NOT VALID TERMINATOR
 1122 246D 8417      NTCOLD:  STY    INBFPT       ; UPDATE INBFPT
 1123 246F 68                 PLA                 ; RESTORE X AND Y
 1124 2470 A8                 TAY
 1125 2471 68                 PLA
 1126 2472 AA                 TAX
 1127 2473 60                 RTS                 ; RETURN
 1128
 1129 2474 A909      NTCOLE:  LDA    #9           ; SIGNAL ER 9 IF ILLEGAL DURATION
 1130 2476 852B               STA    NTERR
 1131 2478 D0F3               BNE    NTCOLD       ; GO RETURN
 1132
 1133                ;        NOTE PITCH TRANSLATE TABLE
 1134
 1135 247A 090A0B    NTPTCT:  .BYTE  9,10,11      ; A@ A A#
 1136 247D 0B0C01             .BYTE  11,12,1      ; B@ B B#
 1137 2480 0C0102             .BYTE  12,1,2       ; C@ C C#
 1138 2483 020304             .BYTE  2,3,4        ; D@ D D#
 1139 2486 040506             .BYTE  4,5,6        ; E@ E E#
 1140 2489 050607             .BYTE  5,6,7        ; F@ F F#
 1141 248C 070809             .BYTE  7,8,9        ; G@ G G#
 1142
 1143                ;        DURATION TRANSLATE TABLES
 1144
 1145                ;        LIST OF VALID DURATION LETTERS
 1146 248F 574851    NTDURT:  .BYTE  'W','H','Q'  ; WHOLE, HALF, QUARTER
 1147 2492 455354             .BYTE  'E','S','T'  ; EIGHTH, SIXTEENTH, THIRTY-SECOND
 1148 2495 00                 .BYTE  0            ; END OF TABLE MARKER
 1149
 1150                ;        TRANSLATE DURATION LETTER AND MODIFIER TO DURATION CODE
 1151 2496 000100    NTDRT1:  .BYTE  0,1,0        ; W. (ILLEGAL)  W   W3 (ILLEGAL)
 1152 2499 020305             .BYTE  2,3,5        ; H.   H   H3
 1153 249C 040608             .BYTE  4,6,8        ; Q.   Q   Q3
 1154 249F 07090B             .BYTE  7,9,11       ; E.   E   E3
 1155 24A2 0A0C0E             .BYTE  10,12,14     ; S.   S   S3
 1156 24A5 0D0F00             .BYTE  13,15,0      ; T.   T   T3 (ILLEGAL)
 1157
 1158                ;        TRANSLATE DURATION CODE TO ABSOLUTE TIME VALUE
 1159 24A8 C09060    NTDRT2:  .BYTE  192,144,96   ; W   H.  W3
 1160 24AB 484030             .BYTE  72,64,48     ; Q.  H3  Q
 1161 24AE 242018             .BYTE  36,32,24     ; E.  Q3  E
 1162 24B1 12100C             .BYTE  18,16,12     ; S.  E3  S
 1163 24B4 090806             .BYTE  9,8,6        ; T.  S3  T
 1164
                              .PAGE  'NUMBER COLLECT ROUTINE'
 1165                ;        NCOL   NUMBER COLLECT
 1166                ;               ENTER WITH INBFPT POINTING TO A DIGIT.
 1167                ;               EXIT WITH NMERR=0, NUMBER VALUE IN NBF, AND INBFPT
 1168                ;               POINTING TO TERMINATOR (FIRST NON-DIGIT) IF A VALID
 1169                ;               NUMBER FOUND (0-255).
 1170                ;               EXIT WITH NMERR=ER1, NBF UNDEFINED, AND INBFPT POINTING
 1171                ;               TO TERMINATOR IF OVERFLOW.
 1172                ;               NO REGISTERS BOTHERED
 1173
 1174 24B7 48        NCOL:    PHA                 ; SAVE A
 1175 24B8 98                 TYA                 ; SAVE Y
 1176 24B9 48                 PHA
 1177 24BA A900               LDA    #0
 1178 24BC 8522               STA    NBF          ; CLEAR NUMBER BEING FORMED
 1179 24BE 852C               STA    NMERR        ; CLEAR OVERFLOW FLAG
 1180 24C0 A417               LDY    INBFPT
 1181 24C2 B100      NCOL1:   LDA    (INBFA),Y    ; GET CHARACTER FROM INPUT BUFFER
 1182 24C4 C930               CMP    #'0'
 1183 24C6 9024               BCC    NCOL3        ; BRANCH IF NOT A DIGIT
 1184 24C8 C93A               CMP    #'9'+1
 1185 24CA B020               BCS    NCOL3        ; BRANCH IF NOT A DIGIT
 1186 24CC A522               LDA    NBF          ; IF A DIGIT, FIRST MULTIPLY NBF BY 10
 1187 24CE 0A                 ASLA                ; WITH OVERFLOW CHECK
 1188 24CF B021               BCS    NCOL4
 1189 24D1 0A                 ASLA
 1190 24D2 B01E               BCS    NCOL4
 1191 24D4 6522               ADC    NBF
 1192 24D6 B01A               BCS    NCOL4
 1193 24D8 0A                 ASLA
 1194 24D9 B017               BCS    NCOL4
 1195 24DB 8522               STA    NBF
 1196 24DD B100               LDA    (INBFA),Y    ; GET NEW DIGIT
 1197 24DF 69D0               ADC    #-'0'        ; CONVERT TO BINARY
 1198 24E1 18                 CLC
 1199 24E2 6522               ADC    NBF          ; ADD TO NBF WITH OVERFLOW CHECK
 1200 24E4 B00C               BCS    NCOL4
 1201 24E6 8522               STA    NBF
 1202 24E8 C8        NCOL2:   INY                 ; GO TO NEXT INPUT CHARACTER
 1203 24E9 4CC224             JMP    NCOL1        ; GO FOR NEXT DIGIT
 1204
 1205 24EC 8417      NCOL3:   STY    INBFPT       ; UPDATE INBFPT
 1206 24EE 68                 PLA                 ; RESTORE REGISTERS
 1207 24EF A8                 TAY
 1208 24F0 68                 PLA
 1209 24F1 60                 RTS                 ; RETURN
 1210
 1211 24F2 A901      NCOL4:   LDA    #1           ; SET OVERFLOW ERROR CODE
 1212 24F4 852C               STA    NMERR
 1213 24F6 4CE824             JMP    NCOL2        ; CONTINUE SCANNING FOR END OF NUMBER
 1214
                              .PAGE  'KEYWORD COLLECT AND MATCH ROUTINE'
 1215                ;        KWCOL  KEYWORD COLLECT AND MATCH
 1216                ;               ENTER WITH INBFPT POINTING TO A NON-BLANK CHARACTER.
 1217                ;               ROUTINE LOOKS AT THIS CHARACTER AND THE NEXT TWO.
 1218                ;               IF THE 3 CHARACTERS MATCH A KEYWORD, RETURN WITH X
 1219                ;               POINTING TO JUMP ADDRESS IN KEYWORD TABLE ENTRY AND C=1
 1220                ;               AND INBFPT POINTING TO CHARACTER FOLLOWING THE KEYWORD.
 1221                ;               IF NO MATCH, RETURN WITH C=0 AND INBFPT UNCHANGED.
 1222                ;               PRESERVES Y
 1223
 1224 24F9 98        KWCOL:   TYA                 ; SAVE Y
 1225 24FA 48                 PHA
 1226 24FB A200               LDX    #0           ; ZERO POINTER INTO KEYWORD TABLE
 1227 24FD 8644      KWCOL1:  STX    KWTP
 1228 24FF BD2925             LDA    KWTAB,X      ; TEST IF AT END OF KEYWORD TABLE
 1229 2502 D003               BNE    KWCOL3       ; JUMP IF NOT
 1230 2504 18                 CLC                 ; CLEAR CARRY TO INDICATE FAILURE TO FIND
 1231 2505 9016               BCC    KWCOL5       ; A MATCH AND RETURN
 1232
 1233 2507 A903      KWCOL3:  LDA    #3           ; INITIALIZE MATCH COUNT
 1234 2509 8545               STA    KWMC
 1235 250B A417               LDY    INBFPT       ; GET POINTER TO CURRENT INPUT CHARACTER
 1236 250D B100      KWCOL4:  LDA    (INBFA),Y    ; TEST IF MATCH BETWEEN KEYWORD TABLE ENTRY
 1237 250F DD2925             CMP    KWTAB,X      ; AND INPUT BUFFER CHARACTER
 1238 2512 D00C               BNE    KWCOL6       ; JUMP IF NO MATCH
 1239 2514 E8                 INX                 ; IF MATCH, INCREMENT INDICES
 1240 2515 C8                 INY
 1241 2516 C645               DEC    KWMC         ; TEST IF 3 CHARACTERS MATCHED
 1242 2518 D0F3               BNE    KWCOL4       ; GO MATCH NEXT IF NOT
 1243 251A 8417               STY    INBFPT       ; IF MATCH, UPDATE INBFPT
 1244 251C 38                 SEC                 ; SET CARRY TO INDICATE MATCH FOUND
 1245 251D 68        KWCOL5:  PLA                 ; RESTORE Y
 1246 251E A8                 TAY
 1247 251F 60                 RTS                 ; RETURN
 1248
 1249 2520 A544      KWCOL6:  LDA    KWTP         ; UPDATE KWTP TO POINT TO NEXT KEYWORD IN
 1250 2522 18                 CLC                 ; TABLE
 1251 2523 6905               ADC    #5
 1252 2525 AA                 TAX
 1253 2526 4CFD24             JMP    KWCOL1       ; GO TRY TO MATCH NEXT KEYWORD
 1254
 1255                ;        KEYWORD TABLE
 1256
 1257 2529 4E5643    KWTAB:   .BYTE  'N','V','C'  ; DEFINE NUMBER OF VOICES
 1258 252C DD21               .WORD  NVCP
 1259 252E 414354             .BYTE  'A','C','T'  ; ACTIVATE VOICES
 1260 2531 F821               .WORD  ACTP
 1261 2533 444354             .BYTE  'D','C','T'  ; DEACTIVATE VOICES
 1262 2536 FC21               .WORD  DCTP
 1263 2538 574156             .BYTE  'W','A','V'  ; ASSIGN WAVEFORM
 1264 253B 3122               .WORD  WAVP
 1265 253D 4A5352             .BYTE  'J','S','R'  ; JUMP TO SUBROUTINE
 1266 2540 5D22               .WORD  JSRP
 1267 2542 4A4D50             .BYTE  'J','M','P'  ; JUMP
 1268 2545 6122               .WORD  JMPP
 1269 2547 525453             .BYTE  'R','T','S'  ; RETURN FROM SUBROUTINE
 1270 254A 9322               .WORD  RTSP
 1271 254C 54504F             .BYTE  'T','P','O'  ; SET TEMPO
 1272 254F B222               .WORD  TPOP
 1273 2551 454E44             .BYTE  'E','N','D'  ; END OF SONG
 1274 2554 9B22               .WORD  ENDP
 1275 2556 414253             .BYTE  'A','B','S'  ; FORCE ABSOLUTE PITCH UPDATE
 1276 2559 C822               .WORD  ABSP
 1277 255B 535542             .BYTE  'S','U','B'  ; DEFINE BEGINNING OF SUBROUTINE AREA
 1278 255E D422               .WORD  SUBP
 1279 2560 455342             .BYTE  'E','S','B'  ; DEFINE END OF SUBROUTINE AREA
 1280 2563 FA22               .WORD  ESBP
 1281 2565 00                 .BYTE  0            ; END OF TABLE MARKER
 1282
                              .PAGE  'SYMBOL TABLE SEARCH'
 1283                ;        SYMSR  SYMBOL TABLE SEARCH
 1284                ;               ENTER WITH SYMBOL TO SEARCH FOR IN A RANGE OF 1-255.
 1285                ;               EXIT WITH SYMTPT POINTING TO LOW BYTE OF SYMBOL VALUE
 1286                ;               AND C=1 IF SYMBOL IS FOUND, SYMTPT POINTING TO END OF
 1287                ;               SYMBOL TABLE AND C=0 IF NOT FOUND.  NO REGISTERS
 1288                ;               AFFECTED.
 1289
 1290 2566 48        SYMSR:   PHA                 ; SAVE A
 1291 2567 8A                 TXA                 ; SAVE X
 1292 2568 48                 PHA
 1293 2569 98                 TYA                 ; SAVE Y
 1294 256A 48                 PHA
 1295 256B A504               LDA    SYMTA        ; COPY SYMTA INTO SYMTPT
 1296 256D 851D               STA    SYMTPT
 1297 256F A505               LDA    SYMTA+1
 1298 2571 851E               STA    SYMTPT+1
 1299 2573 A000               LDY    #0           ; ZERO Y FOR STRAIGHT INDIRECT
 1300 2575 811D      SYMSR0:  LDA    (SYMTPT),Y   ; TEST IF AT END OF SYMBOL TABLE
 1301 2577 D003               BNE    SYMSR1       ; BRANCH IF NOT
 1302 2579 18                 CLC                 ; CLEAR CARRY TO INDICATE SEARCH FAILURE
 1303 257A 900C               BCC    SYMSR2       ; GO RESTORE REGISTERS AND RETURN
 1304 257C BA        SYMSR1:  TSX                 ; LOAD STACK POINTER INTO X TO MAKE SAVED
 1305                                             ; SYMBOL ACCESSABLE
 1306 257D DD0301             CMP    X'103,X      ; COMPARE WITH SYMBOL BEING SEARCHED FOR
 1307 2580 D00C               BNE    SYMSR3       ; BRANCH IF NO MATCH
 1308 2582 A21D               LDX    #SYMTPT      ; INCREMENT SYMTPT TO POINT TO VALUE IF
 1309 2584 20E625             JSR    DINC         ; MATCH
 1310 2587 38                 SEC                 ; SET CARRY TO INDICATE SEARCH SUCCESS
 1311 2588 68        SYMSR2:  PLA                 ; RESTORE REGISTERS
 1312 2589 A8                 TAY
 1313 258A 68                 PLA
 1314 258B AA                 TAX
 1315 258C 68                 PLA
 1316 258D 60                 RTS                 ; RETURN
 1317
 1318 258E A21D      SYMSR3:  LDX    #SYMTPT      ; INCREMENT SYMTPT BY 3 TO POINT TO NEXT
 1319 2590 20E625             JSR    DINC         ; SYMBOL
 1320 2593 206625             JSR    DINC
 1321 2596 206625             JSR    DINC
 1322 2599 4C7525             JMP    SYMSR0       ; GO CHECK NEXT SYMBOL
 1323
                              .PAGE  'SYMBOL TABLE ADD'
 1324                ;        SYMAD  SYMBOL TABLE ADD ROUTINE
 1325                ;               ENTER WITH SYMBOL TO ADD IN A, VALUE OF SYMBOL IN CODEPT
 1326                ;               SYMTPT POINTING TO END OF SYMBOL TABLE
 1327                ;               RETURN WITH C=1 FOR SUCCESSFUL ADD, C=0 FOR SYMBOL TABLE
 1328                ;               OVERFLOW.
 1329
 1330 259C 48        SYMAD:   PHA                 ; SAVE A
 1331 259D 8A                 TXA                 ; SAVE X
 1332 259E 48                 PHA
 1333 259F 98                 TYA                 ; SAVE Y
 1334 25A0 48                 PHA
 1335 25A1 A51F               LDA    SYMTCT       ; TEST FOR TABLE OVERFLOW
 1336 25A3 C50A               CMP    SYMTLN
 1337 25A5 9007               BCC    SYMAD2       ; JUMP IF OK
 1338 25A7 18                 CLC                 ; CLEAR CARRY TO INDICATE UNSUCCESSFUL ADD
 1339 25A8 68        SYMAD1:  PLA                 ; RESTORE REGISTERS
 1340 25A9 A8                 TAY
 1341 25AA 68                 PLA
 1342 25AB AA                 TAX
 1343 25AC 68                 PLA
 1344 25AD 60                 RTS                 ; RETURN
 1345
 1346 25AE E61F      SYMAD2:  INC    SYMTCT       ; INCREMENT SYMBOL COUNT
 1347 25B0 A000               LDY    #0           ; ZERO Y FOR STRAIGHT INDIRECT
 1348 25B2 BA                 TSX                 ; COPY STACK POINTER TO X TO MAKE SAVED
 1349                                             ; SYMBOL ACCESSABLE
 1350 25B3 BD0301             LDA    X'103,X      ; ADD THE SYMBOL TO THE TABLE
 1351 25B6 911D               STA    (SYMTPT),Y
 1352 25B8 A21D               LDX    #SYMTPT      ; INCREMENT SYMBOL TABLE POINTER
 1353 25BA 20E625             JSR    DINC
 1354 25BD A519               LDA    CODEPT       ; ADD LOW VALUE TO TABLE
 1355 25BF 911D               STA    (SYMTPT),Y
 1356 25C1 20E625             JSR    DINC
 1357 25C4 A51A               LDA    CODEPT+1     ; ADD HIGH VALUE TO TABLE
 1358 25C6 911D               STA    (SYMTPT),Y
 1359 25C8 20E625             JSR    DINC
 1360 25CB A900               LDA    #0           ; SET END OF TABLE FLAG
 1361 25CD 911D               STA    (SYMTPT),Y
 1362 25CF 38                 SEC                 ; SET CARRY TO INDICATE SUCCESSFUL ADD
 1363 25D0 B0D6               BCS    SYMAD1       ; GO RESTORE REGISTERS AND RETURN
 1364
                              .PAGE  'MISCELLANEOUS ROUTINES'
 1365                ;        NNB    NEXT NON-BLANK SEARCH
 1366                ;               ENTER WITH INBFPT POINTING TO FIRST BYTE TO BE SEARCHED.
 1367                ;               EXIT WITH INBFPT POINTING TO FIRST NON-BLANK CHARACTER
 1368                ;               ALL REGISTERS PRESERVED
 1369
 1370 25D2 48        NNB:     PHA                 ; SAVE A AND Y
 1371 25D3 98                 TYA
 1372 25D4 48                 PHA
 1373 25D5 A417               LDY    INBFPT       ; GET CURRENT CHARACTER POINTER
 1374 25D7 B100      NNB1:    LDA    (INBFA),Y    ; GET CURRENT CHARACTER
 1375 25D9 C920               CMP    #' '         ; TEST IF A BLANK
 1376 25DB D003               BNE    NNB2         ; JUMP IF NOT
 1377 25DD C8                 INY                 ; IF A BLANK, GO TO NEXT CHARACTER
 1378 25DE D0F7               BNE    NNB1         ; GO TEST NEXT CHARACTER
 1379 25E0 8417      NNB2:    STY    INBFPT       ; RETURN WITH CHARACTER IN A WHEN NON-BLANK
 1380 25E2 68                 PLA                 ; FOUND
 1381 25E3 A8                 TAY                 ; RESTORE Y AND A
 1382 25E4 68                 PLA
 1383 25E5 60                 RTS                 ; RETURN
 1384
 1385
 1386                ;        DINC   DOUBLE INCREMENT
 1387                ;               ENTER WITH X POINTING TO THE LOW BYTE OF THE WORD TO
 1388                ;               INCREMENT, HIGH BYTE IS AT X+1 (WORD IN BASE PAGE)
 1389                ;               NO REGISTERS AFFECTED
 1390
 1391 25E6 F600      DINC:    INC    0,X          ; INCREMENT LOW BYTE
 1392 25E8 D002               BNE    DINC1        ; SKIP AHEAD IF NO CARRY OUT
 1393 25EA F601               INC    1,X
 1394 25EC 60        DINC1:   RTS                 ; RETURN
 1395
 1396
 1397
 1398                ;        LINKS TO USER SUPPLIED INPUT AND OUTPUT ROUTINES
 1399
 1400 25ED 6C0B00    IN:      JMP    (INP)        ; INPUT A LINE TO INPUT BUFFER
 1401 25F0 6C0F00    OU:      JMP    (OUT)        ; OUTPUT A LINE FROM OUTPUT BUFFER
 1402 25F3 6C0D00    INIT:    JMP    (INPINT)     ; INITIALIZE INPUT ROUTINE IN PREP FOR
 1403                                             ; FIRST LINE
 1404 25F6 6C1100    OUIT:    JMP    (OUTINT)     ; INITIALIZE OUTPUT ROUTINE IN PREP FOR
 1405                                             ; FIRST LINE
 1406
 1407                ;        ER     ERROR ROUTINE
 1408                ;               IF LISTING IS ENABLED, A MESSAGE OF THE FORM "ER X" IS
 1409                ;               INSERTED IN THE OBJECT CODE LISTING.
 1410                ;               IF LISTING IS DISABLED, A CALL TO A USER SUPPLIED ERROR
 1411                ;               ROUTINE IS PERFORMED WITH THE ERROR CODE IN A
 1412                ;               NO REGISTERS ARE BOTHERED
 1413
 1414 25F9 48        ER:      PHA                 ; SAVE THE ERROR CODE
 1415 25FA 8A                 TXA                 ; SAVE OTHER REGISTERS IN CASE USER ERROR
 1416 25FB 48                 PHA                 ; ROUTINE USES THEM
 1417 25FC 98                 TYA
 1418 25FD 48                 PHA
 1419 25FE BA                 TSX                 ; COPY STACK POINTER INTO X SO THAT ERROR
 1420                                             ; CODE MAY BE RETRIEVED
 1421 25FF A515               LDA    LIST         ; TEST IF LISTING ENABLED
 1422 2601 F020               BEQ    ER2          ; JUMP AHEAD TO USER ERROR ROUTINE IF NOT
 1423 2603 A945               LDA    #'E'         ; PUT ER- IN LISTING
 1424 2605 205D23             JSR    OUCH
 1425 2608 A952               LDA    #'R'
 1426 260A 205D23             JSR    OUCH
 1427 260D A92D               LDA    #'-'
 1428 260F 205D23             JSR    OUCH
 1429 2612 BD0301             LDA    X'103,X      ; RESTORE ERROR CODE
 1430 2615 203F23             JSR    OUHX         ; OUTPUT AS TWO HEX DIGITS
 1431 2618 A920               LDA    #' '         ; FOLLOWED BY A BLANK
 1432 261A 205D23             JSR    OUCH
 1433 261D 68        ER1:     PLA                 ; RESTORE REGISTERS
 1434 261E A8                 TAY
 1435 261F 68                 PLA
 1436 2620 AA                 TAX
 1437 2621 68                 PLA
 1438 2622 60                 RTS                 ; RETURN
 1439
 1440 2623 BD0301    ER2:     LDA    X'103,X      ; GET ERROR CODE BACK
 1441 2626 202C26             JSR    ER3
 1442 2629 4C1D26             JMP    ER1          ; GO RESTORE REGISTERS AND RETURN
 1443
 1444 262C 6C1300    ER3:     JMP    (ERR)        ; GO TO USER SUPPLIED ERROR ROUTINE
 1445
 1446
 1447 0000           CMPEND:  END
NO ERROR LINES
 
